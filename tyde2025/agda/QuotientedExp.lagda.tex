\begin{code}[hide]
{-# OPTIONS
  --safe
  --cubical
  --guardedness
  --no-import-sorts
  --hidden-argument-puns #-}

module QuotientedExp where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Isomorphism using (Iso; iso; isoToPath)
open import Cubical.Foundations.Univalence using (ua→)
open import Cubical.Foundations.HLevels
open import Cubical.Reflection.RecordEquiv using (declareRecordIsoΣ)
import Cubical.Data.Empty as ⊥
open import Cubical.Data.Maybe
  using
    ( Maybe; nothing; just;
      isOfHLevelMaybe; just-inj; ¬nothing≡just; ¬just≡nothing)

record ConatStr (A : Type) : Type where
  infixl 6 _+_
  infixl 7 _×_
  field
    isSetA : isSet A

    _+_ : A → A → A
    +-assoc : ∀ x y z → (x + y) + z ≡ x + (y + z)
    +-comm : ∀ x y → x + y ≡ y + x

    zero : A
    +-idₗ : ∀ x → zero + x ≡ x

    _×_ : A → A → A
    ×-assoc : ∀ x y z → (x × y) × z ≡ x × (y × z)
    ×-comm : ∀ x y → x × y ≡ y × x

    one : A
    ×-idₗ : ∀ x → one × x ≡ x

    ×-distₗ-+ : ∀ x y z → (x + y) × z ≡ x × z + y × z
    ×-annihₗ : ∀ x → zero × x ≡ zero

  suc : A → A
  suc = one +_

  infixr 8 _^_
  field
    _^_ : A → A → A
    ^-assocᵣ-× : ∀ x y z → x ^ (y × z) ≡ (x ^ z) ^ y
    ^-idᵣ : ∀ x → x ^ one ≡ x

    ^-distᵣ-+ : ∀ x y z → x ^ (y + z) ≡ x ^ z × x ^ y
    ^-annihᵣ : ∀ x → x ^ zero ≡ one

    ^-distₗ-× : ∀ x y z → (x × y) ^ z ≡ x ^ z × y ^ z
    ^-annihₗ : ∀ x → one ^ x ≡ one

    block : A → A → A
    block-assocᵣ-× : ∀ x y z → block x (y × z) ≡ block (block x z) y
    block-idᵣ : ∀ x → block x one ≡ x

    block-distᵣ-+ :
      ∀ x y z → block x (y + z) ≡ block x y + block x z × suc x ^ y
    block-annihᵣ : ∀ x → block x zero ≡ zero

    block-distₗ-× :
      ∀ x y z → block (y + x × suc y) z ≡ block y z + block x z × suc y ^ z
    block-annihₗ : ∀ x → block zero x ≡ zero

    ^-sucₗ : ∀ x y → suc x ^ y ≡ suc (block x y)

    ∞ : A
    +-annihₗ : ∀ x → ∞ + x ≡ ∞
    block-∞ᵣ : ∀ x → block (suc x) ∞ ≡ ∞

  module ConatReasoning where
    infixl 6 _⟩+⟨_
    _⟩+⟨_ : ∀ {x x' y y'} → x ≡ x' → y ≡ y' → x + y ≡ x' + y'
    p ⟩+⟨ q = cong₂ _+_ p q

    +-idᵣ : ∀ x → x + zero ≡ x
    +-idᵣ x =
      x + zero ≡⟨ +-comm _ _ ⟩
      zero + x ≡⟨ +-idₗ _ ⟩
      x        ∎

    module _ {a x y z} (p : x + y ≡ z) where
      +-pullᵣ : a + x + y ≡ a + z
      +-pullᵣ =
        a + x + y   ≡⟨ +-assoc _ _ _ ⟩
        a + (x + y) ≡⟨ refl ⟩+⟨ p ⟩
        a + z       ∎

    module _ {a x y z} (p : x ≡ y + z) where
      +-pushᵣ : a + x ≡ a + y + z
      +-pushᵣ = sym (+-pullᵣ (sym p))

    module _ {a x} (p : zero ≡ x) where
      +-introₗ : a ≡ x + a
      +-introₗ =
        a        ≡⟨ sym (+-idₗ _) ⟩
        zero + a ≡⟨ p ⟩+⟨ refl ⟩
        x + a    ∎

      +-introᵣ : a ≡ a + x
      +-introᵣ =
        a        ≡⟨ sym (+-idᵣ _) ⟩
        a + zero ≡⟨ refl ⟩+⟨ p ⟩
        a + x    ∎

    module _ {a x} (p : x ≡ zero) where
      +-elimₗ : x + a ≡ a
      +-elimₗ = sym (+-introₗ (sym p))

      +-elimᵣ : a + x ≡ a
      +-elimᵣ = sym (+-introᵣ (sym p))

    module _ {a x y z w} (p : x + y ≡ z + w) where
      +-extendᵣ : a + x + y ≡ a + z + w
      +-extendᵣ =
        a + x + y   ≡⟨ +-assoc _ _ _ ⟩
        a + (x + y) ≡⟨ refl ⟩+⟨ p ⟩
        a + (z + w) ≡⟨ sym (+-assoc _ _ _) ⟩
        a + z + w   ∎

    +-sucₗ : ∀ x y → suc x + y ≡ suc (x + y)
    +-sucₗ x y = +-assoc _ _ _

    +-sucᵣ : ∀ x y → x + suc y ≡ suc (x + y)
    +-sucᵣ x y =
      x + suc y   ≡⟨ +-comm _ _ ⟩
      suc y + x   ≡⟨ +-sucₗ _ _ ⟩
      suc (y + x) ≡⟨ cong suc (+-comm _ _) ⟩
      suc (x + y) ∎

    infixl 7 _⟩×⟨_
    _⟩×⟨_ : ∀ {x x' y y'} → x ≡ x' → y ≡ y' → x × y ≡ x' × y'
    p ⟩×⟨ q = cong₂ _×_ p q

    ×-idᵣ : ∀ x → x × one ≡ x
    ×-idᵣ x =
      x × one ≡⟨ ×-comm _ _ ⟩
      one × x ≡⟨ ×-idₗ _ ⟩
      x       ∎

    module _ {a x y z} (p : x × y ≡ z) where
      ×-pullᵣ : a × x × y ≡ a × z
      ×-pullᵣ =
        a × x × y   ≡⟨ ×-assoc _ _ _ ⟩
        a × (x × y) ≡⟨ refl ⟩×⟨ p ⟩
        a × z       ∎

    module _ {a x y z} (p : x ≡ y × z) where
      ×-pushᵣ : a × x ≡ a × y × z
      ×-pushᵣ = sym (×-pullᵣ (sym p))

    module _ {a x} (p : one ≡ x) where
      ×-introᵣ : a ≡ a × x
      ×-introᵣ =
        a       ≡⟨ sym (×-idᵣ _) ⟩
        a × one ≡⟨ refl ⟩×⟨ p ⟩
        a × x   ∎

    module _ {a x} (p : x ≡ one) where
      ×-elimᵣ : a × x ≡ a
      ×-elimᵣ = sym (×-introᵣ (sym p))

    module _ {a x y z w} (p : x × y ≡ z × w) where
      ×-extendᵣ : a × x × y ≡ a × z × w
      ×-extendᵣ =
        a × x × y   ≡⟨ ×-assoc _ _ _ ⟩
        a × (x × y) ≡⟨ refl ⟩×⟨ p ⟩
        a × (z × w) ≡⟨ sym (×-assoc _ _ _) ⟩
        a × z × w   ∎

    ×-annihᵣ : ∀ x → x × zero ≡ zero
    ×-annihᵣ x =
      x × zero ≡⟨ ×-comm _ _ ⟩
      zero × x ≡⟨ ×-annihₗ _ ⟩
      zero     ∎

    ×-sucₗ : ∀ x y → suc x × y ≡ y + x × y
    ×-sucₗ x y =
      (one + x) × y   ≡⟨ ×-distₗ-+ _ _ _ ⟩
      one × y + x × y ≡⟨ ×-idₗ _ ⟩+⟨ refl ⟩
      y + x × y       ∎

    ×-sucᵣ : ∀ x y → x × suc y ≡ x + x × y
    ×-sucᵣ x y =
      x × suc y ≡⟨ ×-comm _ _ ⟩
      suc y × x ≡⟨ ×-sucₗ _ _ ⟩
      x + y × x ≡⟨ refl ⟩+⟨ ×-comm _ _ ⟩
      x + x × y ∎

    one-suc : one ≡ suc zero
    one-suc = sym (+-idᵣ _)

    infixr 8 _⟩^⟨_
    _⟩^⟨_ : ∀ {x x' y y'} → x ≡ x' → y ≡ y' → x ^ y ≡ x' ^ y'
    p ⟩^⟨ q = cong₂ _^_ p q

    ^-sucᵣ : ∀ x y → x ^ suc y ≡ x ^ y × x
    ^-sucᵣ x y =
      x ^ (one + y)   ≡⟨ ^-distᵣ-+ _ _ _ ⟩
      x ^ y × x ^ one ≡⟨ refl ⟩×⟨ ^-idᵣ _ ⟩
      x ^ y × x       ∎

    block-sucᵣ : ∀ x y → block x (suc y) ≡ x + block x y × suc x
    block-sucᵣ x y =
      block x (one + y)                     ≡⟨ block-distᵣ-+ _ _ _ ⟩
      block x one + block x y × suc x ^ one ≡⟨ block-idᵣ _ ⟩+⟨ refl ⟩×⟨ ^-idᵣ _ ⟩
      x + block x y × suc x                 ∎

    +-annihᵣ : ∀ x → x + ∞ ≡ ∞
    +-annihᵣ x =
      x + ∞ ≡⟨ +-comm _ _ ⟩
      ∞ + x ≡⟨ +-annihₗ _ ⟩
      ∞     ∎

    ∞-suc : ∞ ≡ suc ∞
    ∞-suc =
      ∞       ≡⟨ sym (+-annihₗ _) ⟩
      ∞ + one ≡⟨ +-comm _ _ ⟩
      one + ∞ ∎

    unpred : Maybe A → A
    unpred nothing = zero
    unpred (just x') = suc x'

record ConatStrPred (A : Type) (pred : A → Maybe A) : Type where
  field
    conatStr : ConatStr A

  open ConatStr conatStr public
  open ConatReasoning

  field
    pred-zero : pred zero ≡ nothing
    pred-suc : ∀ x → pred (suc x) ≡ just x
    unpred-pred : ∀ x → unpred (pred x) ≡ x

module E where
  record NExpr : Type
  data Expr : Type
  suc : Expr → Expr

  record NExpr where
    coinductive
    field pred : Maybe Expr

  open NExpr public

  infixl 6 _+_
  infixl 7 _×_
  infixr 8 _^_
\end{code}
Now we modify the proof to add exponentiation and its related equations. We have
to extend our previous language by adding them as constructors.
\begin{AgdaAlign}
\begin{AgdaSuppressSpace}
\begin{code}
  data Expr where
\end{code}
\begin{code}[hide]
    isSetExpr : isSet Expr

    _+_ : Expr → Expr → Expr
    +-assoc : ∀ x y z → (x + y) + z ≡ x + (y + z)
    +-comm : ∀ x y → x + y ≡ y + x

    zero : Expr
    +-idₗ : ∀ x → zero + x ≡ x

    _×_ : Expr → Expr → Expr
    ×-assoc : ∀ x y z → (x × y) × z ≡ x × (y × z)
    ×-comm : ∀ x y → x × y ≡ y × x

    one : Expr
    ×-idₗ : ∀ x → one × x ≡ x

    ×-distₗ-+ : ∀ x y z → (x + y) × z ≡ x × z + y × z
    ×-annihₗ : ∀ x → zero × x ≡ zero
\end{code}
\begin{code}
    _^_         : Expr → Expr → Expr
    ^-assocᵣ-×  : ∀ x y z → x ^ (y × z) ≡ (x ^ z) ^ y
    ^-idᵣ       : ∀ x → x ^ one ≡ x

    ^-distᵣ-+   : ∀ x y z → x ^ (y + z) ≡ x ^ z × x ^ y
    ^-annihᵣ    : ∀ x → x ^ zero ≡ one

    ^-distₗ-×   : ∀ x y z → (x × y) ^ z ≡ x ^ z × y ^ z
    ^-annihₗ    : ∀ x → one ^ x ≡ one
\end{code}
\end{AgdaSuppressSpace}

However, the current method requires us to specify what the predecessor of
exponentiating two numbers is. To do this, we create a new operation that
represents a ``block'' of digits, essentially representing $y$ number of the
digit $x$ in base $x + 1$ (so $x$ is always the last digit in the base). This
gives us the following equation:
\[
  \AgdaInductiveConstructor{block}\ x\ y = \sum_{i=0}^{y-1} x \times (1 + x)^i = (1+x)^y - 1
\]
For example if we want to calculate $10^3$ using the \AgdaInductiveConstructor{block}, we can
do it like so:
\[
  \AgdaInductiveConstructor{block}\ 9\ 3 = 9 \times 10^0 + 9 \times 10^1 + 9 \times 10^2 = 999
\]
Then adding one gets us $1000$.

We add this \AgdaInductiveConstructor{block} operation as a constructor to the
language, and some equations that correspond to the ones for exponentiation.
\begin{code}
    block : Expr → Expr → Expr
    block-assocᵣ-× :
      ∀ x y z → block x (y × z) ≡ block (block x z) y
    block-idᵣ : ∀ x → block x one ≡ x

    block-distᵣ-+ :
      ∀ x y z →
      block x (y + z) ≡ block x y + block x z × suc x ^ y
    block-annihᵣ : ∀ x → block x zero ≡ zero

    block-distₗ-× :
      ∀ x y z →
      block (y + x × suc y) z ≡
      block y z + block x z × suc y ^ z
    block-annihₗ : ∀ x → block zero x ≡ zero
\end{code}
We also add the equation that relates exponentiation and
\AgdaInductiveConstructor{block}.
\begin{code}
    ^-sucₗ : ∀ x y → suc x ^ y ≡ suc (block x y)
\end{code}
Finally, we can add infinity as a constructor and add some properties about it.
For example, the last equation allows us to prove $x^\infty = \infty$ for $x \ge
2$.
\begin{code}
    ∞ : Expr
    +-annihₗ : ∀ x → ∞ + x ≡ ∞
    block-∞ᵣ : ∀ x → block (suc x) ∞ ≡ ∞
\end{code}
\begin{code}[hide]
    trans : ∀ {x y z} → x ≡ y → y ≡ z → x ≡ z

    embed : NExpr → Expr
    embed-zero : ∀ x → x .pred ≡ nothing → embed x ≡ zero
    embed-suc : ∀ x x' → x .pred ≡ just x' → embed x ≡ suc x'
\end{code}
\end{AgdaAlign}
\begin{code}[hide]
  suc = one +_

  conatStrExpr : ConatStr Expr
  conatStrExpr = record{ isSetA = isSetExpr; Expr }

  open ConatStr.ConatReasoning conatStrExpr

  data IsPred : Maybe Expr → Expr → Type where
    nothing : IsPred nothing zero
    just : ∀ x' → IsPred (just x') (suc x')

  isPropIsPred-zero :
    ∀ {x' x} (p : nothing ≡ x') (q : zero ≡ x) r →
    PathP (λ i → IsPred (p i) (q i)) nothing r
  isPropIsPred-zero p q nothing =
    subst2 (λ e₁ e₂ → PathP (λ i → IsPred (e₁ i) (e₂ i)) nothing nothing)
      (isOfHLevelMaybe 0 isSetExpr _ _ refl p)
      (isSetExpr _ _ refl q)
      refl
  isPropIsPred-zero p q (just x') = ⊥.rec (¬nothing≡just p)

  isPropIsPred-suc :
    ∀ {x' x} x'′ (p : just x'′ ≡ x') (q : suc x'′ ≡ x) r →
    PathP (λ i → IsPred (p i) (q i)) (just x'′) r
  isPropIsPred-suc x'′ p q nothing = ⊥.rec (¬just≡nothing p)
  isPropIsPred-suc x'′ p q (just x') =
    subst2 (λ e₁ e₂ → PathP (λ i → IsPred (e₁ i) (e₂ i)) (just x'′) (just x'))
      (isOfHLevelMaybe 0 isSetExpr _ _ (cong just (just-inj _ _ p)) p)
      (isSetExpr _ _ (cong suc (just-inj _ _ p)) q)
      (cong IsPred.just (just-inj _ _ p))

  isPropIsPred : ∀ x' x → isProp (IsPred x' x)
  isPropIsPred _ _ nothing q = isPropIsPred-zero refl refl q
  isPropIsPred _ _ (just x') q = isPropIsPred-suc x' refl refl q

  record Pred (x : Expr) : Type where
    field
      pred : Maybe Expr
      isPred : IsPred pred x

  open Pred
  unquoteDecl PredIsoΣ = declareRecordIsoΣ PredIsoΣ (quote Pred)

  Pred-path :
    ∀ {x y} {p : x ≡ y} {xᴾ yᴾ} →
    xᴾ .pred ≡ yᴾ .pred → PathP (λ i → Pred (p i)) xᴾ yᴾ
  Pred-path q i .pred = q i
  Pred-path {p} {xᴾ} {yᴾ} q i .isPred =
    isProp→PathP (λ i → isPropIsPred (q i) (p i)) (xᴾ .isPred) (yᴾ .isPred) i

  isSetPred : ∀ x → isSet (Pred x)
  isSetPred x =
    isOfHLevelRetractFromIso 2 PredIsoΣ
      (isSetΣ (isOfHLevelMaybe 0 isSetExpr) λ x' →
        isProp→isSet (isPropIsPred _ _))

  +-pred : Maybe Expr → Expr → Maybe Expr → Maybe Expr
  +-pred nothing y y' = y'
  +-pred (just x') y y' = just (x' + y)

  +-isPred :
    ∀ {x x' y y'} →
    IsPred x' x → IsPred y' y → IsPred (+-pred x' y y') (x + y)
  +-isPred {y} nothing p = subst (IsPred _) (sym (+-idₗ _)) p
  +-isPred {y} (just x') p =
    subst (IsPred _) (sym (+-sucₗ _ _)) (just (x' + y))

  infixl 6 _+ᴾ_
  _+ᴾ_ : ∀ {x y} → Pred x → Pred y → Pred (x + y)
  _+ᴾ_ {y} xᴾ yᴾ .pred = +-pred (xᴾ .pred) y (yᴾ .pred)
  (xᴾ +ᴾ yᴾ) .isPred = +-isPred (xᴾ .isPred) (yᴾ .isPred)

  +-assoc-pred :
    ∀ x' y y' z z' →
    +-pred (+-pred x' y y') z z' ≡ +-pred x' (y + z) (+-pred y' z z')
  +-assoc-pred nothing y y' z z' = refl
  +-assoc-pred (just x') y y' z z' = congS just (+-assoc _ _ _)

  +-assocᴾ :
    ∀ {x y z} xᴾ yᴾ zᴾ →
    PathP (λ i → Pred (+-assoc x y z i)) ((xᴾ +ᴾ yᴾ) +ᴾ zᴾ) (xᴾ +ᴾ (yᴾ +ᴾ zᴾ))
  +-assocᴾ {y} {z} xᴾ yᴾ zᴾ =
    Pred-path (+-assoc-pred (xᴾ .pred) y (yᴾ .pred) z (zᴾ .pred))

  +-comm-pred :
    ∀ {x x' y y'} → IsPred x' x → IsPred y' y →
    +-pred x' y y' ≡ +-pred y' x x'
  +-comm-pred nothing nothing = refl
  +-comm-pred nothing (just y') = congS just (sym (+-idᵣ _))
  +-comm-pred (just x') nothing = congS just (+-idᵣ _)
  +-comm-pred (just x') (just y') =
    congS just
      ( x' + suc y'   ≡⟨ +-sucᵣ _ _ ⟩
        suc (x' + y') ≡⟨ cong suc (+-comm _ _) ⟩
        suc (y' + x') ≡⟨ sym (+-sucᵣ _ _) ⟩
        y' + suc x'   ∎)

  +-commᴾ :
    ∀ {x y} xᴾ yᴾ → PathP (λ i → Pred (+-comm x y i)) (xᴾ +ᴾ yᴾ) (yᴾ +ᴾ xᴾ)
  +-commᴾ xᴾ yᴾ = Pred-path (+-comm-pred (xᴾ .isPred) (yᴾ .isPred))

  zeroᴾ : Pred zero
  zeroᴾ .pred = nothing
  zeroᴾ .isPred = nothing

  +-idₗᴾ : ∀ {x} xᴾ → PathP (λ i → Pred (+-idₗ x i)) (zeroᴾ +ᴾ xᴾ) xᴾ
  +-idₗᴾ xᴾ = Pred-path refl

  ×-pred : Maybe Expr → Expr → Maybe Expr → Maybe Expr
  ×-pred nothing y y' = nothing
  ×-pred (just x') y nothing = nothing
  ×-pred (just x') y (just y') = just (y' + x' × y)

  ×-isPred :
    ∀ {x x' y y'} →
    IsPred x' x → IsPred y' y → IsPred (×-pred x' y y') (x × y)
  ×-isPred nothing p = subst (IsPred _) (sym (×-annihₗ _)) nothing
  ×-isPred (just x') nothing = subst (IsPred _) (sym (×-annihᵣ _)) nothing
  ×-isPred {y} (just x') (just y') =
    subst (IsPred _)
      ( suc (y' + x' × y) ≡⟨ sym (+-sucₗ _ _) ⟩
        suc y' + x' × y   ≡⟨⟩
        y + x' × y         ≡⟨ sym (×-sucₗ _ _) ⟩
        suc x' × y         ∎)
      (just (y' + x' × y))

  infixl 7 _×ᴾ_
  _×ᴾ_ : ∀ {x y} → Pred x → Pred y → Pred (x × y)
  _×ᴾ_ {y} xᴾ yᴾ .pred = ×-pred (xᴾ .pred) y (yᴾ .pred)
  (xᴾ ×ᴾ yᴾ) .isPred = ×-isPred (xᴾ .isPred) (yᴾ .isPred)

  ×-assoc-pred :
    ∀ x' y y' z z' →
    ×-pred (×-pred x' y y') z z' ≡ ×-pred x' (y × z) (×-pred y' z z')
  ×-assoc-pred nothing y y' z z' = refl
  ×-assoc-pred (just x') y nothing z z' = refl
  ×-assoc-pred (just x') y (just y') z nothing = refl
  ×-assoc-pred (just x') y (just y') z (just z') =
    congS just
      ( z' + (y' + x' × y) × z     ≡⟨ +-pushᵣ (×-distₗ-+ _ _ _) ⟩
        z' + y' × z + x' × y × z   ≡⟨ refl ⟩+⟨ ×-assoc _ _ _ ⟩
        z' + y' × z + x' × (y × z) ∎)

  ×-assocᴾ : ∀ {x y z} xᴾ yᴾ zᴾ →
    PathP (λ i → Pred (×-assoc x y z i)) ((xᴾ ×ᴾ yᴾ) ×ᴾ zᴾ) (xᴾ ×ᴾ (yᴾ ×ᴾ zᴾ))
  ×-assocᴾ {x} {y} {z} xᴾ yᴾ zᴾ =
    Pred-path (×-assoc-pred (xᴾ .pred) y (yᴾ .pred) z (zᴾ .pred))

  ×-comm-pred :
    ∀ {x x' y y'} → IsPred x' x → IsPred y' y →
    ×-pred x' y y' ≡ ×-pred y' x x'
  ×-comm-pred nothing nothing = refl
  ×-comm-pred nothing (just y') = refl
  ×-comm-pred (just x') nothing = refl
  ×-comm-pred (just x') (just y') =
    congS just
      ( y' + x' × suc y'   ≡⟨ +-pushᵣ (×-sucᵣ _ _) ⟩
        y' + x' + x' × y' ≡⟨ +-comm _ _ ⟩+⟨ ×-comm _ _ ⟩
        x' + y' + y' × x' ≡⟨ +-pullᵣ (sym (×-sucᵣ _ _)) ⟩
        x' + y' × suc x' ∎)

  ×-commᴾ :
    ∀ {x y} xᴾ yᴾ → PathP (λ i → Pred (×-comm x y i)) (xᴾ ×ᴾ yᴾ) (yᴾ ×ᴾ xᴾ)
  ×-commᴾ xᴾ yᴾ = Pred-path (×-comm-pred (xᴾ .isPred) (yᴾ .isPred))

  oneᴾ : Pred one
  oneᴾ .pred = just zero
  oneᴾ .isPred = subst (IsPred _) (sym one-suc) (just zero)

  ×-idₗ-pred : ∀ x x' → ×-pred (just zero) x x' ≡ x'
  ×-idₗ-pred x nothing = refl
  ×-idₗ-pred x (just x') = congS just (+-elimᵣ (×-annihₗ _))

  ×-idₗᴾ : ∀ {x} xᴾ → PathP (λ i → Pred (×-idₗ x i)) (oneᴾ ×ᴾ xᴾ) xᴾ
  ×-idₗᴾ {x} xᴾ = Pred-path (×-idₗ-pred x (xᴾ .pred))

  ×-distₗ-+-pred :
    ∀ x' y y' z z' →
    ×-pred (+-pred x' y y') z z' ≡
    +-pred (×-pred x' z z') (y × z) (×-pred y' z z')
  ×-distₗ-+-pred nothing y y' z z' = refl
  ×-distₗ-+-pred (just x) y y' z (just z') =
    congS just (+-pushᵣ (×-distₗ-+ _ _ _))
  ×-distₗ-+-pred (just x) y nothing z nothing = refl
  ×-distₗ-+-pred (just x) y (just y') z nothing = refl

  ×-distₗ-+ᴾ :
    ∀ {x y z} xᴾ yᴾ zᴾ →
    PathP (λ i → Pred (×-distₗ-+ x y z i))
      ((xᴾ +ᴾ yᴾ) ×ᴾ zᴾ) (xᴾ ×ᴾ zᴾ +ᴾ yᴾ ×ᴾ zᴾ)
  ×-distₗ-+ᴾ {y} {z} xᴾ yᴾ zᴾ =
    Pred-path (×-distₗ-+-pred (xᴾ .pred) y (yᴾ .pred) z (zᴾ .pred))

  ×-annihₗᴾ : ∀ {x} xᴾ → PathP (λ i → Pred (×-annihₗ x i)) (zeroᴾ ×ᴾ xᴾ) zeroᴾ
  ×-annihₗᴾ xᴾ = Pred-path refl

  sucᴾ : ∀ {x} → Pred x → Pred (suc x)
  sucᴾ = oneᴾ +ᴾ_
\end{code}
We then add the methods for the constructor we added. Here we define the
predecessor of exponentiation using the aforementioned
\AgdaInductiveConstructor{block} constructor.
\begin{code}
  ^-pred :
    Maybe Expr → Expr → Maybe Expr → Maybe Expr
  ^-pred x'         y nothing    = just zero
  ^-pred nothing    y (just y')  = nothing
  ^-pred (just x')  y (just y')  = just (block x' y)
\end{code}
\begin{code}[hide]
  ^-isPred :
    ∀ {x x' y y'} →
    IsPred x' x → IsPred y' y → IsPred (^-pred x' y y') (x ^ y)
  ^-isPred {x} p nothing =
    subst (IsPred _)
      ( suc zero ≡⟨ sym one-suc ⟩
        one      ≡⟨ sym (^-annihᵣ _) ⟩
        x ^ zero ∎)
      (just zero)
  ^-isPred nothing (just y') =
    subst (IsPred _)
      ( zero             ≡⟨ sym (×-annihᵣ _) ⟩
        zero ^ y' × zero ≡⟨ sym (^-sucᵣ _ _) ⟩
        zero ^ suc y'    ∎)
      nothing
  ^-isPred {y} (just x') (just y') =
    subst (IsPred _) (sym (^-sucₗ _ _)) (just (block x' y))

  infixr 8 _^ᴾ_
  _^ᴾ_ : ∀ {x y} → Pred x → Pred y → Pred (x ^ y)
  _^ᴾ_ {y} xᴾ yᴾ .pred = ^-pred (xᴾ .pred) y (yᴾ .pred)
  (xᴾ ^ᴾ yᴾ) .isPred = ^-isPred (xᴾ .isPred) (yᴾ .isPred)

  ^-assocᵣ-×-pred :
    ∀ x' y y' z z' →
    ^-pred x' (y × z) (×-pred y' z z') ≡ ^-pred (^-pred x' z z') y y'
  ^-assocᵣ-×-pred x' y nothing z z' = refl
  ^-assocᵣ-×-pred x' y (just y') z nothing = congS just (sym (block-annihₗ _))
  ^-assocᵣ-×-pred nothing y (just y') z (just z') = refl
  ^-assocᵣ-×-pred (just x') y (just y') z (just z') =
    congS just (block-assocᵣ-× _ _ _)

  ^-assocᵣ-×ᴾ :
    ∀ {x y z} xᴾ yᴾ zᴾ →
    PathP (λ i → Pred (^-assocᵣ-× x y z i))
      (xᴾ ^ᴾ (yᴾ ×ᴾ zᴾ)) ((xᴾ ^ᴾ zᴾ) ^ᴾ yᴾ)
  ^-assocᵣ-×ᴾ {x} {y} {z} xᴾ yᴾ zᴾ =
    Pred-path (^-assocᵣ-×-pred (xᴾ .pred) y (yᴾ .pred) z (zᴾ .pred))

  ^-idᵣ-pred : ∀ x' → ^-pred x' one (just zero) ≡ x'
  ^-idᵣ-pred nothing = refl
  ^-idᵣ-pred (just x') = congS just (block-idᵣ _)

  ^-idᵣᴾ : ∀ {x} xᴾ → PathP (λ i → Pred (^-idᵣ x i)) (xᴾ ^ᴾ oneᴾ) xᴾ
  ^-idᵣᴾ xᴾ = Pred-path (^-idᵣ-pred (xᴾ .pred))

  ^-distᵣ-+-pred :
    ∀ {x x' y y' z z'} → IsPred x' x → IsPred y' y → IsPred z' z →
    ^-pred x' (y + z) (+-pred y' z z') ≡
    ×-pred (^-pred x' z z') (x ^ y) (^-pred x' y y')
  ^-distᵣ-+-pred p nothing nothing =
    congS just (+-introᵣ (sym (×-annihₗ _)))
  ^-distᵣ-+-pred nothing nothing (just z') = refl
  ^-distᵣ-+-pred {x} {z} (just x') nothing (just z') =
    congS just
      ( block x' (zero + z)          ≡⟨ cong₂ block refl (+-idₗ _) ⟩
        block x' z                   ≡⟨ ×-introᵣ (sym (^-annihᵣ _)) ⟩
        block x' z × x ^ zero        ≡⟨ sym (+-idₗ _) ⟩
        zero + block x' z × x ^ zero ∎)
  ^-distᵣ-+-pred nothing (just y') nothing = refl
  ^-distᵣ-+-pred nothing (just y') (just x') = refl
  ^-distᵣ-+-pred {x} {y} (just x') (just y') nothing =
    congS just
      ( block x' (y + zero)       ≡⟨ cong₂ block refl (+-idᵣ _) ⟩
        block x' y                ≡⟨ +-introᵣ (sym (×-annihₗ _)) ⟩
        block x' y + zero × x ^ y ∎)
  ^-distᵣ-+-pred (just x') (just y') (just z') =
    congS just (block-distᵣ-+ _ _ _)

  ^-distᵣ-+ᴾ :
    ∀ {x y z} xᴾ yᴾ zᴾ →
    PathP (λ i → Pred (^-distᵣ-+ x y z i))
      (xᴾ ^ᴾ (yᴾ +ᴾ zᴾ)) (xᴾ ^ᴾ zᴾ ×ᴾ xᴾ ^ᴾ yᴾ)
  ^-distᵣ-+ᴾ xᴾ yᴾ zᴾ =
    Pred-path (^-distᵣ-+-pred (xᴾ .isPred) (yᴾ .isPred) (zᴾ .isPred))

  ^-annihᵣᴾ : ∀ {x} xᴾ → PathP (λ i → Pred (^-annihᵣ x i)) (xᴾ ^ᴾ zeroᴾ) oneᴾ
  ^-annihᵣᴾ xᴾ = Pred-path refl

  ^-distₗ-×-pred :
    ∀ {y y'} x' → IsPred y' y → ∀ z z' →
    ^-pred (×-pred x' y y') z z' ≡
    ×-pred (^-pred x' z z') (y ^ z) (^-pred y' z z')
  ^-distₗ-×-pred x' p z nothing = congS just (+-introᵣ (sym (×-annihₗ _)))
  ^-distₗ-×-pred nothing p z (just z') = refl
  ^-distₗ-×-pred (just x') nothing z (just z') = refl
  ^-distₗ-×-pred {y} (just x') (just y') z (just z') =
    congS just (block-distₗ-× _ _ _)

  ^-distₗ-×ᴾ :
    ∀ {x y z} xᴾ yᴾ zᴾ →
    PathP (λ i → Pred (^-distₗ-× x y z i))
      ((xᴾ ×ᴾ yᴾ) ^ᴾ zᴾ) (xᴾ ^ᴾ zᴾ ×ᴾ yᴾ ^ᴾ zᴾ)
  ^-distₗ-×ᴾ {z} xᴾ yᴾ zᴾ =
    Pred-path (^-distₗ-×-pred (xᴾ .pred) (yᴾ .isPred) z (zᴾ .pred))

  ^-annihₗ-pred : ∀ x x' → ^-pred (just zero) x x' ≡ just zero
  ^-annihₗ-pred x nothing = refl
  ^-annihₗ-pred x (just x') = congS just (block-annihₗ _)

  ^-annihₗᴾ : ∀ {x} xᴾ → PathP (λ i → Pred (^-annihₗ x i)) (oneᴾ ^ᴾ xᴾ) oneᴾ
  ^-annihₗᴾ {x} xᴾ = Pred-path (^-annihₗ-pred x (xᴾ .pred))
\end{code}

We can also define the predecessor of \AgdaInductiveConstructor{block} itself
using addition and multiplication from the langauge. For example, for the
predecessor of $\AgdaInductiveConstructor{block}\ 9\ 3 = 999$ we would get
$(9-1) + 99 * (1+9) = 8 + 99 * 10$.
\begin{code}
  block-pred :
    Expr → Maybe Expr → Maybe Expr → Maybe Expr
  block-pred x nothing    y'         = nothing
  block-pred x (just x')  nothing    = nothing
  block-pred x (just x')  (just y')  =
    just (x' + block x y' × suc x)
\end{code}
The operations and equations that we have in \AgdaDatatype{Expr} are enough to
define the rest of the methods.
\begin{code}[hide]
  block-isPred :
    ∀ {x x' y y'} → IsPred x' x → IsPred y' y →
    IsPred (block-pred x x' y') (block x y)
  block-isPred nothing p = subst (IsPred _) (sym (block-annihₗ _)) nothing
  block-isPred (just x') nothing =
    subst (IsPred _) (sym (block-annihᵣ _)) nothing
  block-isPred {x} (just x') (just y') =
    subst (IsPred _)
      ( suc (x' + block x y' × suc x) ≡⟨ sym (+-sucₗ _ _) ⟩
        suc x' + block x y' × suc x   ≡⟨⟩
        x + block x y' × suc x         ≡⟨ sym (block-sucᵣ _ _) ⟩
        block x (suc y')               ∎)
      (just (x' + block x y' × suc x))

  blockᴾ : ∀ {x y} → Pred x → Pred y → Pred (block x y)
  blockᴾ {x} xᴾ yᴾ .pred = block-pred x (xᴾ .pred) (yᴾ .pred)
  blockᴾ xᴾ yᴾ .isPred = block-isPred (xᴾ .isPred) (yᴾ .isPred)

  block-assocᵣ-×-pred :
    ∀ {z z'} x x' y' → IsPred z' z →
    block-pred x x' (×-pred y' z z') ≡
    block-pred (block x z) (block-pred x x' z') y'
  block-assocᵣ-×-pred x nothing y' p = refl
  block-assocᵣ-×-pred x (just x') nothing nothing = refl
  block-assocᵣ-×-pred x (just x') nothing (just z') = refl
  block-assocᵣ-×-pred x (just x') (just y') nothing = refl
  block-assocᵣ-×-pred {z} x (just x') (just y') (just z') =
    congS just
      ( x' + block x (z' + y' × z) × suc x                                      ≡⟨ refl ⟩+⟨ block-distᵣ-+ _ _ _ ⟩×⟨ refl ⟩
        x' + (block x z' + block x (y' × z) × suc x ^ z') × suc x               ≡⟨ +-pushᵣ (×-distₗ-+ _ _ _) ⟩
        x' + block x z' × suc x + block x (y' × z) × suc x ^ z' × suc x         ≡⟨ refl ⟩+⟨ ×-pullᵣ (sym (^-sucᵣ _ _)) ⟩
        x' + block x z' × suc x + block x (y' × z) × suc x ^ suc z'             ≡⟨ refl ⟩+⟨ block-assocᵣ-× _ _ _ ⟩×⟨ ^-sucₗ _ _ ⟩
        x' + block x z' × suc x + block (block x z) y' × suc (block x (suc z')) ∎)

  block-assocᵣ-×ᴾ :
    ∀ {x y z} xᴾ yᴾ zᴾ →
    PathP (λ i → Pred (block-assocᵣ-× x y z i))
      (blockᴾ xᴾ (yᴾ ×ᴾ zᴾ)) (blockᴾ (blockᴾ xᴾ zᴾ) yᴾ)
  block-assocᵣ-×ᴾ {x} xᴾ yᴾ zᴾ =
    Pred-path (block-assocᵣ-×-pred x (xᴾ .pred) (yᴾ .pred) (zᴾ .isPred) )

  block-idᵣ-pred : ∀ x x' → block-pred x x' (just zero) ≡ x'
  block-idᵣ-pred x nothing = refl
  block-idᵣ-pred x (just x') =
    congS just
      ( x' + block x zero × suc x ≡⟨ refl ⟩+⟨ block-annihᵣ _ ⟩×⟨ refl ⟩
        x' + zero × suc x         ≡⟨ +-elimᵣ (×-annihₗ _) ⟩
        x'                        ∎)

  block-idᵣᴾ : ∀ {x} xᴾ → PathP (λ i → Pred (block-idᵣ x i)) (blockᴾ xᴾ oneᴾ) xᴾ
  block-idᵣᴾ {x} xᴾ = Pred-path (block-idᵣ-pred x (xᴾ .pred))

  block-distᵣ-+-pred :
    ∀ {y y'} x x' → IsPred y' y → ∀ z z' →
    block-pred x x' (+-pred y' z z') ≡
    +-pred
      (block-pred x x' y')
      (block x z × suc x ^ y)
      (×-pred
        (block-pred x x' z')
        (suc x ^ y)
        (^-pred (just (zero + x)) y y'))
  block-distᵣ-+-pred x nothing y' z z' = refl
  block-distᵣ-+-pred x (just x') nothing z nothing = refl
  block-distᵣ-+-pred x (just x') nothing z (just z') =
    congS just
      ( x' + block x z' × suc x                         ≡⟨ ×-introᵣ (sym (^-annihᵣ _)) ⟩
        (x' + block x z' × suc x) × suc x ^ zero        ≡⟨ sym (+-idₗ _) ⟩
        zero + (x' + block x z' × suc x) × suc x ^ zero ∎)
  block-distᵣ-+-pred x (just x') (just y') z z' =
    congS just
      ( x' + block x (y' + z) × suc x                             ≡⟨ refl ⟩+⟨ block-distᵣ-+ _ _ _ ⟩×⟨ refl ⟩
        x' + (block x y' + block x z × suc x ^ y') × suc x       ≡⟨ +-pushᵣ (×-distₗ-+ _ _ _) ⟩
        x' + block x y' × suc x + block x z × suc x ^ y' × suc x ≡⟨ refl ⟩+⟨ ×-pullᵣ (sym (^-sucᵣ _ _)) ⟩
        x' + block x y' × suc x + block x z × suc x ^ suc y'     ∎)

  block-distᵣ-+ᴾ :
    ∀ {x y z} xᴾ yᴾ zᴾ →
    PathP (λ i → Pred (block-distᵣ-+ x y z i))
      (blockᴾ xᴾ (yᴾ +ᴾ zᴾ)) (blockᴾ xᴾ yᴾ +ᴾ blockᴾ xᴾ zᴾ ×ᴾ sucᴾ xᴾ ^ᴾ yᴾ)
  block-distᵣ-+ᴾ {x} {z} xᴾ yᴾ zᴾ =
    Pred-path (block-distᵣ-+-pred x (xᴾ .pred) (yᴾ .isPred) z (zᴾ .pred))

  block-annihᵣ-pred : ∀ x x' → block-pred x x' nothing ≡ nothing
  block-annihᵣ-pred x nothing = refl
  block-annihᵣ-pred x (just x') = refl

  block-annihᵣᴾ :
    ∀ {x} xᴾ → PathP (λ i → Pred (block-annihᵣ x i)) (blockᴾ xᴾ zeroᴾ) zeroᴾ
  block-annihᵣᴾ {x} xᴾ = Pred-path (block-annihᵣ-pred x (xᴾ .pred))

  block-distₗ-×-pred :
    ∀ {y y' z z'} x x' → IsPred y' y → IsPred z' z →
    block-pred
      (y + x × suc y)
      (+-pred y' (x × suc y) (×-pred x' (suc y) (just (zero + y))))
      z' ≡
    +-pred
      (block-pred y y' z')
      (block x z × suc y ^ z)
      (×-pred
        (block-pred x x' z')
        (suc y ^ z)
        (^-pred (just (zero + y)) z z'))
  block-distₗ-×-pred {y} x x' (just y') (just z') =
    congS just
      ( y' + x × suc y + block (y + x × suc y) z' × suc (y + x × suc y)                                   ≡⟨ refl ⟩+⟨ refl ⟩×⟨ sym (+-sucₗ _ _) ⟩
        y' + x × suc y + block (y + x × suc y) z' × (suc y + x × suc y)                                   ≡⟨ refl ⟩+⟨ ×-pushᵣ (sym (×-sucₗ _ _)) ⟩
        y' + x × suc y + block (y + x × suc y) z' × suc x × suc y                                         ≡⟨ refl ⟩+⟨ block-distₗ-× _ _ _ ⟩×⟨ refl ⟩×⟨ refl ⟩
        y' + x × suc y + (block y z' + block x z' × suc y ^ z') × suc x × suc y                         ≡⟨ refl ⟩+⟨ (refl ⟩+⟨ refl ⟩×⟨ ^-sucₗ _ _) ⟩×⟨ refl ⟩×⟨ refl ⟩
        y' + x × suc y + (block y z' + block x z' × suc (block y z')) × suc x × suc y                   ≡⟨ +-extendᵣ (q _ _ _ _) ⟩
        y' + (x + block y z' + block y z' × x) × suc y + block x z' × suc (block y z') × suc x × suc y ≡⟨ refl ⟩+⟨ (+-comm _ _ ⟩+⟨ ×-comm _ _) ⟩×⟨ refl ⟩+⟨ ×-extendᵣ (×-comm _ _) ⟩×⟨ refl ⟩
        y' + (block y z' + x + x × block y z') × suc y + block x z' × suc x × suc (block y z') × suc y ≡⟨ +-extendᵣ (sym (q _ _ _ _)) ⟩
        y' + block y z' × suc y + (x + block x z' × suc x) × suc (block y z') × suc y                   ≡⟨ refl ⟩+⟨ sym (block-sucᵣ _ _) ⟩×⟨ sym (^-sucₗ _ _) ⟩×⟨ refl ⟩
        y' + block y z' × suc y + block x (suc z') × suc y ^ z' × suc y                                 ≡⟨ refl ⟩+⟨ ×-pullᵣ (sym (^-sucᵣ _ _)) ⟩
        y' + block y z' × suc y + block x (suc z') × suc y ^ suc z'                                     ∎)
    where
    q :
      ∀ x y z w →
      x × y + (w + z × suc w) × suc x × y ≡
      (x + w + w × x) × y + z × suc w × suc x × y
    q x y z w =
      x × y + (w + z × suc w) × suc x × y               ≡⟨ refl ⟩+⟨ ×-assoc _ _ _ ⟩
      x × y + (w + z × suc w) × (suc x × y)             ≡⟨ +-pushᵣ (×-distₗ-+ _ _ _) ⟩
      x × y + w × (suc x × y) + z × suc w × (suc x × y) ≡⟨ refl ⟩+⟨ sym (×-assoc _ _ _) ⟩+⟨ sym (×-assoc _ _ _) ⟩
      x × y + w × suc x × y + z × suc w × suc x × y     ≡⟨ sym (×-distₗ-+ _ _ _) ⟩+⟨ refl ⟩
      (x + w × suc x) × y + z × suc w × suc x × y       ≡⟨ +-pushᵣ (×-sucᵣ _ _) ⟩×⟨ refl ⟩+⟨ refl ⟩
      (x + w + w × x) × y + z × suc w × suc x × y       ∎
  block-distₗ-×-pred x nothing (just y') nothing = refl
  block-distₗ-×-pred x (just x') (just y') nothing = refl
  block-distₗ-×-pred x nothing nothing p = refl
  block-distₗ-×-pred x (just x') nothing nothing = refl
  block-distₗ-×-pred {z} x (just x') nothing (just z') =
    congS just
      ( zero + zero + x' × suc zero + block (zero + x × suc zero) z' × suc (zero + x × suc zero) ≡⟨ +-elimₗ (+-idₗ _) ⟩+⟨ cong₂ block (+-idₗ _) refl ⟩×⟨ cong suc (+-idₗ _) ⟩
        x' × suc zero + block (x × suc zero) z' × suc (x × suc zero)                             ≡⟨ ×-elimᵣ (sym one-suc) ⟩+⟨ cong₂ block (×-elimᵣ (sym one-suc)) refl ⟩×⟨ cong suc (×-elimᵣ (sym one-suc)) ⟩
        x' + block x z' × suc x                                                                  ≡⟨ ×-introᵣ (sym (^-annihₗ _)) ⟩
        (x' + block x z' × suc x) × one ^ z                                                      ≡⟨ +-introₗ (sym (block-annihₗ _)) ⟩
        block zero z + (x' + block x z' × suc x) × one ^ z                                       ≡⟨ cong₂ block (sym (+-idₗ _)) refl ⟩+⟨ refl ⟩×⟨ one-suc ⟩^⟨ refl ⟩
        block (zero + zero) z + (x' + block x z' × suc x) × suc zero ^ z                         ∎)

  block-distₗ-×ᴾ :
    ∀ {x y z} xᴾ yᴾ zᴾ →
    PathP (λ i → Pred (block-distₗ-× x y z i))
      (blockᴾ (yᴾ +ᴾ xᴾ ×ᴾ sucᴾ yᴾ) zᴾ)
      (blockᴾ yᴾ zᴾ +ᴾ blockᴾ xᴾ zᴾ ×ᴾ sucᴾ yᴾ ^ᴾ zᴾ)
  block-distₗ-×ᴾ {x} xᴾ yᴾ zᴾ =
    Pred-path (block-distₗ-×-pred x (xᴾ .pred) (yᴾ .isPred) (zᴾ .isPred))

  block-annihₗᴾ :
    ∀ {x} xᴾ → PathP (λ i → Pred (block-annihₗ x i)) (blockᴾ zeroᴾ xᴾ) zeroᴾ
  block-annihₗᴾ xᴾ = Pred-path refl

  ^-sucₗ-pred :
    ∀ {y y'} x → IsPred y' y → ^-pred (just (zero + x)) y y' ≡ just (zero + block x y)
  ^-sucₗ-pred x nothing = congS just (+-introᵣ (sym (block-annihᵣ _)))
  ^-sucₗ-pred {y} x (just y') =
    congS just
      ( block (zero + x) y ≡⟨ cong₂ block (+-idₗ _) refl ⟩
        block x y          ≡⟨ sym (+-idₗ _) ⟩
        zero + block x y   ∎)

  ^-sucₗᴾ :
    ∀ {x y} xᴾ yᴾ →
    PathP (λ i → Pred (^-sucₗ x y i)) (sucᴾ xᴾ ^ᴾ yᴾ) (sucᴾ (blockᴾ xᴾ yᴾ))
  ^-sucₗᴾ {x} xᴾ yᴾ = Pred-path (^-sucₗ-pred x (yᴾ .isPred))

  ∞ᴾ : Pred ∞
  ∞ᴾ .pred = just ∞
  ∞ᴾ .isPred = subst (IsPred _) (sym ∞-suc) (just ∞)

  +-annihₗᴾ : ∀ {x} xᴾ → PathP (λ i → Pred (+-annihₗ x i)) (∞ᴾ +ᴾ xᴾ) ∞ᴾ
  +-annihₗᴾ xᴾ = Pred-path (congS just (+-annihₗ _))

  block-∞ᵣᴾ :
    ∀ {x} xᴾ → PathP (λ i → Pred (block-∞ᵣ x i)) (blockᴾ (sucᴾ xᴾ) ∞ᴾ) ∞ᴾ
  block-∞ᵣᴾ {x} xᴾ =
    Pred-path
      (congS just
        ( zero + x + block (suc x) ∞ × suc (suc x) ≡⟨ +-idₗ _ ⟩+⟨ block-∞ᵣ x ⟩×⟨ refl ⟩
          x + ∞ × suc (suc x)                      ≡⟨ +-pushᵣ (×-sucᵣ _ _) ⟩
          x + ∞ + ∞ × suc x                      ≡⟨ +-annihᵣ _ ⟩+⟨ refl ⟩
          ∞ + ∞ × suc x                          ≡⟨ +-annihₗ _ ⟩
          ∞                                        ∎))

  transᴾ :
    ∀ {x y z} {p : x ≡ y} {q : y ≡ z} {xᴾ yᴾ zᴾ} →
    PathP (λ i → Pred (p i)) xᴾ yᴾ → PathP (λ i → Pred (q i)) yᴾ zᴾ →
    PathP (λ i → Pred (trans p q i)) xᴾ zᴾ
  transᴾ pᴾ qᴾ = Pred-path ((λ i → pᴾ i .pred) ∙ (λ i → qᴾ i .pred))

  embed-isPred : ∀ x x' → x .pred ≡ x' → IsPred x' (embed x)
  embed-isPred x nothing p = subst (IsPred _) (sym (embed-zero x p)) nothing
  embed-isPred x (just x') p =
    subst (IsPred _) (sym (embed-suc x x' p)) (just x')

  embedᴾ : ∀ x → Pred (embed x)
  embedᴾ x .pred = x .pred
  embedᴾ x .isPred = embed-isPred x (x .pred) refl

  embed-zeroᴾ : ∀ x p → PathP (λ i → Pred (embed-zero x p i)) (embedᴾ x) zeroᴾ
  embed-zeroᴾ x p = Pred-path p

  embed-sucᴾ :
    ∀ {x'} x x'ᴾ p →
    PathP (λ i → Pred (embed-suc x x' p i)) (embedᴾ x) (sucᴾ x'ᴾ)
  embed-sucᴾ {x'} x x'ᴾ p = Pred-path
    ( x .pred           ≡⟨ p ⟩
      just x'          ≡⟨ congS just (sym (+-idₗ _)) ⟩
      just (zero + x') ∎)

  ⟦_⟧ᴾ : ∀ x → Pred x
  ⟦ isSetExpr x y p q i j ⟧ᴾ =
    isOfHLevel→isOfHLevelDep 2 (λ x → isSetPred x)
      ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ (cong ⟦_⟧ᴾ p) (cong ⟦_⟧ᴾ q) (isSetExpr _ _ _ _) i j

  ⟦ x + y ⟧ᴾ = ⟦ x ⟧ᴾ +ᴾ ⟦ y ⟧ᴾ
  ⟦ +-assoc x y z i ⟧ᴾ = +-assocᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ ⟦ z ⟧ᴾ i
  ⟦ +-comm x y i ⟧ᴾ = +-commᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ i
  ⟦ zero ⟧ᴾ = zeroᴾ
  ⟦ +-idₗ x i ⟧ᴾ = +-idₗᴾ ⟦ x ⟧ᴾ i

  ⟦ x × y ⟧ᴾ = ⟦ x ⟧ᴾ ×ᴾ ⟦ y ⟧ᴾ
  ⟦ ×-assoc x y z i ⟧ᴾ = ×-assocᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ ⟦ z ⟧ᴾ i
  ⟦ ×-comm x y i ⟧ᴾ = ×-commᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ i
  ⟦ one ⟧ᴾ = oneᴾ
  ⟦ ×-idₗ x i ⟧ᴾ = ×-idₗᴾ ⟦ x ⟧ᴾ i
  ⟦ ×-distₗ-+ x y z i ⟧ᴾ = ×-distₗ-+ᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ ⟦ z ⟧ᴾ i
  ⟦ ×-annihₗ x i ⟧ᴾ = ×-annihₗᴾ ⟦ x ⟧ᴾ i

  ⟦ x ^ y ⟧ᴾ = ⟦ x ⟧ᴾ ^ᴾ ⟦ y ⟧ᴾ
  ⟦ ^-assocᵣ-× x y z i ⟧ᴾ = ^-assocᵣ-×ᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ ⟦ z ⟧ᴾ i
  ⟦ ^-idᵣ x i ⟧ᴾ = ^-idᵣᴾ ⟦ x ⟧ᴾ i
  ⟦ ^-distᵣ-+ x y z i ⟧ᴾ = ^-distᵣ-+ᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ ⟦ z ⟧ᴾ i
  ⟦ ^-annihᵣ x i ⟧ᴾ = ^-annihᵣᴾ ⟦ x ⟧ᴾ i
  ⟦ ^-distₗ-× x y z i ⟧ᴾ = ^-distₗ-×ᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ ⟦ z ⟧ᴾ i
  ⟦ ^-annihₗ x i ⟧ᴾ = ^-annihₗᴾ ⟦ x ⟧ᴾ i

  ⟦ block x y ⟧ᴾ = blockᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ
  ⟦ block-assocᵣ-× x y z i ⟧ᴾ = block-assocᵣ-×ᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ ⟦ z ⟧ᴾ i
  ⟦ block-idᵣ x i ⟧ᴾ = block-idᵣᴾ ⟦ x ⟧ᴾ i
  ⟦ block-distᵣ-+ x y z i ⟧ᴾ = block-distᵣ-+ᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ ⟦ z ⟧ᴾ i
  ⟦ block-annihᵣ x i ⟧ᴾ = block-annihᵣᴾ ⟦ x ⟧ᴾ i
  ⟦ block-distₗ-× x y z i ⟧ᴾ = block-distₗ-×ᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ ⟦ z ⟧ᴾ i
  ⟦ block-annihₗ x i ⟧ᴾ = block-annihₗᴾ ⟦ x ⟧ᴾ i
  ⟦ ^-sucₗ x y i ⟧ᴾ = ^-sucₗᴾ ⟦ x ⟧ᴾ ⟦ y ⟧ᴾ i

  ⟦ ∞ ⟧ᴾ = ∞ᴾ
  ⟦ +-annihₗ x i ⟧ᴾ = +-annihₗᴾ ⟦ x ⟧ᴾ i
  ⟦ block-∞ᵣ x i ⟧ᴾ = block-∞ᵣᴾ ⟦ x ⟧ᴾ i

  ⟦ trans p q i ⟧ᴾ = transᴾ (cong ⟦_⟧ᴾ p) (cong ⟦_⟧ᴾ q) i
  ⟦ embed x ⟧ᴾ = embedᴾ x
  ⟦ embed-zero x p i ⟧ᴾ = embed-zeroᴾ x p i
  ⟦ embed-suc x x' p i ⟧ᴾ = embed-sucᴾ x ⟦ x' ⟧ᴾ p i

  predᴱ : Expr → Maybe Expr
  predᴱ x = ⟦ x ⟧ᴾ .pred

  isPredᴱ : ∀ x → IsPred (predᴱ x) x
  isPredᴱ x = ⟦ x ⟧ᴾ .isPred

  pred-zero : predᴱ zero ≡ nothing
  pred-zero = refl

  pred-suc : ∀ x → predᴱ (suc x) ≡ just x
  pred-suc x = congS just (+-idₗ _)

  unpred-pred-pred : ∀ {x x'} → IsPred x' x → unpred x' ≡ x
  unpred-pred-pred nothing = refl
  unpred-pred-pred (just x') = refl

  unpred-pred : ∀ x → unpred (predᴱ x) ≡ x
  unpred-pred x = unpred-pred-pred (isPredᴱ x)

  conatStrPredExpr : ConatStrPred Expr predᴱ
  conatStrPredExpr =
    record
      { conatStr = conatStrExpr;
        pred-zero = pred-zero;
        pred-suc = pred-suc;
        unpred-pred = unpred-pred }

  interp : Expr → NExpr
  interp x .pred = predᴱ x

  interp-embed : ∀ x → interp (embed x) ≡ x
  interp-embed x i .pred = x .pred

  embed-interp : ∀ x → embed (interp x) ≡ x
  embed-interp-pred : ∀ {x x'} → IsPred x' x → embed (interp x) ≡ x

  embed-interp x = embed-interp-pred (isPredᴱ x)
  embed-interp-pred nothing = embed-zero (interp zero) refl
  embed-interp-pred (just x') =
    embed-suc (interp (suc x')) x' (congS just (+-idₗ _))

  ExprIsoNExpr : Iso Expr NExpr
  ExprIsoNExpr = iso interp embed interp-embed embed-interp

open E using (NExpr; Expr; pred; predᴱ; conatStrPredExpr)

record ℕ∞ : Type where
  coinductive
  field pred : Maybe ℕ∞

open ℕ∞ public

embed : ℕ∞ → Expr
embedᴺ : ℕ∞ → NExpr
embed-pred : Maybe ℕ∞ → Maybe Expr

embed x = E.embed (embedᴺ x)
embedᴺ x .pred = embed-pred (x .pred)
embed-pred nothing = nothing
embed-pred (just x') = just (embed x')

interp : Expr → ℕ∞
interp-pred : Maybe Expr → Maybe ℕ∞

interp x .pred = interp-pred (predᴱ x)
interp-pred nothing = nothing
interp-pred (just x') = just (interp x')

interp-embed : ∀ x → interp (embed x) ≡ x
interp-embed-pred : ∀ x' → interp-pred (embed-pred x') ≡ x'

interp-embed x i .pred = interp-embed-pred (x .pred) i
interp-embed-pred nothing = refl
interp-embed-pred (just x') = cong just (interp-embed x')

embed-interp : ∀ x → embed (interp x) ≡ x
embed-interpᴺ : ∀ x → embedᴺ (interp x) ≡ E.interp x
embed-interp-pred : ∀ x' → embed-pred (interp-pred x') ≡ x'

embed-interp x = E.trans (cong E.embed (embed-interpᴺ x)) (E.embed-interp x)
embed-interpᴺ x i .pred = embed-interp-pred (predᴱ x) i
embed-interp-pred nothing = refl
embed-interp-pred (just x') = cong just (embed-interp x')

ExprIsoConat : Iso Expr ℕ∞
ExprIsoConat = iso interp embed interp-embed embed-interp

conatStrPredConat : ConatStrPred ℕ∞ pred
conatStrPredConat =
  transport
    (cong₂ ConatStrPred
      (isoToPath ExprIsoConat)
      (ua→ λ x → toPathP (subst-Maybe _)))
    conatStrPredExpr
  where
  subst-Maybe :
    ∀ x' → subst Maybe (isoToPath ExprIsoConat) x' ≡ interp-pred x'
  subst-Maybe nothing = refl
  subst-Maybe (just x') = cong just (transportRefl _)

open ConatStrPred conatStrPredConat
  renaming (isSetA to isSetConat; conatStr to conatStrConat) public
open ConatReasoning public

private
\end{code}

The rest of the proof remains the same, we just add the new operations and
equations to the \AgdaRecord{ℕ∞Str} record. The result is that we have the proof
that \AgdaRecord{ℕ∞} forms an exponential commutative semiring. Since we are
using Cubical Agda, the usage of the univalence principle
computes~\cite{vezzosi-cubical}, the only problem is that there are some
extraneous transports in the result of the computation, but they are invisible
if one only uses the interface provided by the exponential commutative semiring
and the predecessor. Here we show an example of defining $2^\infty$ and proving
that $2^\infty = \infty$.
\begin{code}
  example : ℕ∞
  example = suc (suc zero) ^ ∞

  _ : example ≡ ∞
  _ =
    suc (suc zero) ^ ∞        ≡⟨ ^-sucₗ _ _ ⟩
    suc (block (suc zero) ∞)  ≡⟨ cong suc (block-∞ᵣ _) ⟩
    suc ∞                     ≡⟨ sym ∞-suc ⟩
    ∞                         ∎
\end{code}
\begin{code}[hide]
  _ : one .pred ≡ just zero
  _ =
    one .pred      ≡⟨ cong pred one-suc ⟩
    suc zero .pred ≡⟨ pred-suc _ ⟩
    just zero      ∎
\end{code}

In this section we have seen how to add exponentiation to the proof, however, we
had to modify the original \AgdaDatatype{Expr} datatype. As such, this method is
not modular, as it is inconvenient to define and prove more operations and
equations without modifying the original datatype.

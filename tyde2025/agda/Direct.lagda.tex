\begin{code}[hide]
{-# OPTIONS --cubical --guardedness --no-import-sorts --no-postfix-projections --allow-unsolved-metas #-}

module Direct where

open import Cubical.Foundations.Prelude
open import Cubical.Data.Empty
  renaming (rec to exfalso)
open import Cubical.Data.Maybe using (Maybe; nothing; just; just-inj; ¬just≡nothing)
import Cubical.Data.Nat as ℕ
open ℕ using (ℕ)
open import Introduction using (ℕ∞; pred; zero; suc; pred-inj)
open import Problem using (_+_; +-match)
\end{code}
In this section, we show some examples of how to do corecursion without running
into the guardedness issue by coming up with specific internal states for each
corecursion, which allows us to define the functions in a guarded form.

To avoid the guardedness issue in Section~\ref{sec:problem}, we define
multiplication from scratch instead of reusing addition. First, we match to
check whether either of the arguments is zero.
\begin{code}[hide]
infixl 7 _×_ _×'_
mutual
\end{code}
\begin{code}
  _×_ : ℕ∞ → ℕ∞ → ℕ∞
  pred (x × y) = ×-match (pred x) (pred y)

  ×-match : Maybe ℕ∞ → Maybe ℕ∞ → Maybe ℕ∞
  ×-match nothing    y'         = nothing
  ×-match (just x')  nothing    = nothing
  ×-match (just x')  (just y')  = just (x' ×' y')
\end{code}
For the non-zero case, we define a separate operation \AgdaFunction{\_\times'\_}
such that $x\times'y = (x+1)\times(y+1)-1$. The idea for the function is to
count $y+1$ steps $x+1$ times. This means that after each $y+1$ steps, we have
to reset the counter. To achieve this, we define a helper function which keeps
track of the original $y$ which we call~$y_0$.
\begin{code}
  _×'_ : ℕ∞ → ℕ∞ → ℕ∞
  x ×' y = ×'-helper x y y

  ×'-helper : ℕ∞ → ℕ∞ → ℕ∞ → ℕ∞
  pred (×'-helper x y y₀) =
    ×'-helper-match x (pred x) (pred y) y₀

  ×'-helper-match :
    ℕ∞ → Maybe ℕ∞ → Maybe ℕ∞ → ℕ∞ →
    Maybe ℕ∞
  ×'-helper-match x x' (just y') y₀ =
    just (×'-helper x y' y₀)
  ×'-helper-match x (just x') nothing y₀ =
    just (×'-helper x' y₀ y₀)
  ×'-helper-match x nothing nothing y₀ =
    nothing
\end{code}
If $y$ is not zero, we continue by decreasing it by one. If $y$ is zero but $x$
is non-zero, then we decrease $x$ by one and reset $y$ to~$y_0$. If both are
zero then we stop. As an example, if we compute $3 \times' 2$, then we get the
following trace for $(x, y)$:
\[
  (3,2) \to (3,1) \to (3,0) \to (2,2) \to \dots \to (0,1) \to (0,0)
\]
It takes $4 \times 3 - 1 = 11$ steps for it to terminate.

Exponentiation can be defined as well. We can first define a
\AgdaFunction{\_\textasciicircum'\_} such that $x \mathop{\textasciicircum}' y =
(x+1)^{y+1}-1$, then define a helper function with the following type
corecursively:
\begin{code}[hide]
postulate
  ADMIT : ∀ {ℓ} {A : Type ℓ} → A

record NEList∞ (A : Type) : Type where

replicate : ∀ {A} → ℕ∞ → A → NEList∞ A
replicate = ADMIT

infixr 5 _∷_
_∷_ : ∀ {A} → A → NEList∞ A → NEList∞ A
_∷_ = ADMIT

mutual
\end{code}
\begin{code}
  _^'_ : ℕ∞ → ℕ∞ → ℕ∞
  x ^' y = ^'-helper (y ∷ replicate y x) x ℕ.zero

  ^'-helper : NEList∞ ℕ∞ → ℕ∞ → ℕ → ℕ∞
\end{code}
\begin{code}[hide]
  ^'-helper = ADMIT
\end{code}
In the helper function, \AgdaRecord{NEList∞} is a nonempty colist (potentially
infinite list), the first argument is an iterated version of what was done when
defining multiplication and the second argument is the resetting value. The
following is an example trace of $2 \mathop{\textasciicircum}' 2$
\begin{gather*}
  [2, 2, 2] \to [1, 2, 2] \to [0, 2, 2] \to [2, 1, 2] \to \dots \\
  \qquad\qquad\qquad\qquad\qquad\qquad\to [1, 0, 0] \to [0, 0, 0]
\end{gather*}
When some prefix of the colist is filled with zeroes, we need to recurse into
the colist to find the next number to decrease, but because colist is
coinductive and potentially infinite, as the last argument we keep an inductive
natural number to track how deep we can go and use it to recurse into the
colist.

To prove the commutativity of addition, we keep track of how many steps we have
taken so far in the internal state of the corecursion using a finite natural
number. For this, we introduce an operation for adding a finite natural number
to a conatural number.
\begin{code}
infixl 6 _+ₗ_
_+ₗ_ : ℕ → ℕ∞ → ℕ∞
ℕ.zero +ₗ x = x
ℕ.suc n +ₗ x = suc (n +ₗ x)
\end{code}
We also prove that the successor operation can be moved from the right side of
addition to the front of the whole exression by simple induction on the natural
number.
\begin{code}
+ₗ-suc : ∀ n x → n +ₗ suc x ≡ suc (n +ₗ x)
\end{code}
\begin{code}[hide]
+ₗ-suc ℕ.zero x = refl
+ₗ-suc (ℕ.suc n) x = cong suc (+ₗ-suc n x)
\end{code}
Now we prove the commutativity of addition which has the following type
signature:
\begin{code}[hide]
mutual
\end{code}
\begin{code}
  +-comm : ∀ x y → x + y ≡ y + x
\end{code}

We illustrate the intuition of the proof using the diagram below. The top line
is the left hand side of \AgdaFunction{+-comm}, and the bottom line is the right
hand side. We use $x_0$ and $y_0$ to denote the starting $x$ and $y$ values. At
the beginning, we set $n$ to be $0$, then at each step, if neither $x$ or $y$
has ended, we decrease them and increase $n$ by one. The diagram illustrates the
middle of this process. As we go through $x$ and $y$, we learn that both of them
must be at least~$n$, and during the proof we keep the proof that adding $n$ to
$x$ and $y$ gives us back the original values ($x_0$ and $y_0$).
\begin{center}
  \begin{tikzpicture}[xscale=8, yscale=.5]
    \draw (0,1) -- (1,1);
    \draw (0,0) -- (1,0);

    \draw (0,0.75) -- (0,1.25);
    \draw ($(1/3,0.75)$) -- ($(1/3,1.25)$);
    \draw (1,0.75) -- (1,1.25);

    \draw (0,-0.25) -- (0,0.25);
    \draw ($(2/3,-0.25)$) -- ($(2/3,0.25)$);
    \draw (1,-0.25) -- (1,0.25);

    \draw [-{To[length=1mm]}] (0,1) -- ($(2/9,1)$);
    \draw [-{To[length=1mm]}] (0,0) -- ($(2/9,0)$);

    \draw [decorate, decoration={brace, amplitude=2mm}]
    (0,1.75) -- ($(2/9,1.75)$) node [midway, above=2mm] {$n$};
    \draw [decorate, decoration={brace, amplitude=2mm}]
    ($(2/9,1.75)$) -- ($(1/3,1.75)$) node [midway, above=2mm] {$x$};
    \draw [decorate, decoration={brace, amplitude=2mm}]
    ($(1/3,1.75)$) -- (1,1.75) node [midway, above=2mm] {$n+y=y_0$};

    \draw [decorate, decoration={brace, mirror, amplitude=2mm}]
    ($(2/9,-0.75)$) -- ($(2/3,-0.75)$) node [midway, below=2mm] {$y$};
    \draw [decorate, decoration={brace, mirror, amplitude=2mm}]
    ($(2/3,-0.75)$) -- (1,-0.75) node [midway, below=2mm] {$n+x=x_0$};
  \end{tikzpicture}
\end{center}
When we reach the end of~$x$, we learn that it is finite and equals to some~$n$.
We continue stepping through $y$ on both sides.
\begin{center}
  \begin{tikzpicture}[xscale=8, yscale=.5]
    \draw (0,1) -- (1,1);
    \draw (0,0) -- (1,0);

    \draw (0,0.75) -- (0,1.25);
    \draw ($(1/3,0.75)$) -- ($(1/3,1.25)$);
    \draw (1,0.75) -- (1,1.25);

    \draw (0,-0.25) -- (0,0.25);
    \draw ($(2/3,-0.25)$) -- ($(2/3,0.25)$);
    \draw (1,-0.25) -- (1,0.25);

    \draw [-{To[length=1mm]}] (0,1) -- ($(4/9,1)$);
    \draw [-{To[length=1mm]}] (0,0) -- ($(4/9,0)$);

    \draw [decorate, decoration={brace, amplitude=2mm}]
    (0,1.75) -- ($(1/3,1.75)$) node [midway, above=2mm] {$n$};
    \draw [decorate, decoration={brace, amplitude=2mm}]
    ($(4/9,1.75)$) -- (1,1.75) node [midway, above=2mm] {$n+y=y_0$};

    \draw [decorate, decoration={brace, mirror, amplitude=2mm}]
    ($(4/9,-0.75)$) -- ($(2/3,-0.75)$) node [midway, below=2mm] {$y$};
    \draw [decorate, decoration={brace, mirror, amplitude=2mm}]
    ($(2/3,-0.75)$) -- (1,-0.75) node [midway, below=2mm] {$n+0$};
  \end{tikzpicture}
\end{center}
When we reach the end of~$y$, we only have $n$ on both sides, which we can
immediately prove equal.

In the formalisation, we have two helper lemmas that correspond to the two
diagrams. During corecursion, we generalise the resulting equation and add
equality arguments to avoid using the transitivity operation after the
corecursive call, since transitivity does not preserve guardedness in Agda. We
omit the uninteresting equality proofs below for readability, abbreviating them
with their type (e.g.\ \AgdaFunction{y₀≡x₀} is a proof with type $\AgdaBound{y₀}
\mathrel{\AgdaFunction{≡}} \AgdaBound{x₀}$).
\begin{AgdaSuppressSpace}
\begin{code}
  +-comm x y = +-comm-helper₁ ℕ.zero x x y y refl refl

  +-comm-helper₁ :
    ∀ n x x₀ y y₀ → n +ₗ x ≡ x₀ → n +ₗ y ≡ y₀ →
    x + y₀ ≡ y + x₀
  pred (+-comm-helper₁ n x x₀ y y₀ nx ny i) =
    +-comm-helper₁-match n x (pred x) x₀ y (pred y) y₀
      refl refl nx ny i

  +-comm-helper₁-match :
    ∀ n x x' x₀ y y' y₀ →
    pred x ≡ x' → pred y ≡ y' →
    n +ₗ x ≡ x₀ → n +ₗ y ≡ y₀ →
    +-match x' y₀ ≡ +-match y' x₀
  +-comm-helper₁-match
    n x nothing x₀ y nothing y₀ px py nx ny =
      cong pred y₀≡x₀
\end{code}
\begin{code}[hide]
      where
      y₀≡x₀ = sym ny ∙ cong (n +ₗ_) (pred-inj (py ∙ sym px)) ∙ nx
\end{code}
\begin{code}
  +-comm-helper₁-match
    n x nothing x₀ y (just y') y₀ px py nx ny =
      cong pred y₀≡1+n+y' ∙
      cong just
        (+-comm-helper₂ n y' (n +ₗ y') refl ∙ y'+n+0≡y'+x₀)
\end{code}
\begin{code}[hide]
    where
    y₀≡1+n+y' = sym ny ∙ cong (n +ₗ_) (pred-inj py) ∙ +ₗ-suc n y'
    y'+n+0≡y'+x₀ = cong (y' +_) (cong (n +ₗ_) (pred-inj (sym px)) ∙ nx)
  +-comm-helper₁-match
    n x (just x') x₀ y nothing y₀ px py nx ny =
      sym (+-comm-helper₁-match n y nothing y₀ x (just x') x₀ py px ny nx)
\end{code}
\begin{code}
  +-comm-helper₁-match
    n x (just x') x₀ y (just y') y₀ px py nx ny =
      cong just
        (+-comm-helper₁ (ℕ.suc n) x' x₀ y' y₀
          1+n+x'≡x₀ 1+n+y'≡y₀)
\end{code}
\begin{code}[hide]
    where
    1+n+x'≡x₀ = sym (+ₗ-suc n x') ∙ cong (n +ₗ_) (pred-inj (sym px)) ∙ nx
    1+n+y'≡y₀ = sym (+ₗ-suc n y') ∙ cong (n +ₗ_) (pred-inj (sym py)) ∙ ny
\end{code}
\end{AgdaSuppressSpace}
In the first branch, both $x$ and $y$ reach zero at the same time, at which
point we can already prove that both sides are equal. In the second branch,
$x$~reaches zero first, which is why we switch to the other lemma. There is a
symmetric case where $y$ reaches zero first which we omit. The last case is
where we have the corecursive call, stepping through both $x$ and $y$.

In the second lemma, we similarly have to generalise the equation to avoid the
transitivity operation. The second branch is an impossible case where we have a
contradiction from the equation assumptions.
\begin{AgdaSuppressSpace}
\begin{code}
  +-comm-helper₂ :
    ∀ n y y₀ → n +ₗ y ≡ y₀ → y₀ ≡ y + (n +ₗ zero)
  pred (+-comm-helper₂ n y y₀ ny i) =
    +-comm-helper₂-match
      n y (pred y) y₀ (pred y₀) refl refl ny i

  +-comm-helper₂-match :
    ∀ n y y' y₀ y₀' →
    pred y ≡ y' → pred y₀ ≡ y₀' → n +ₗ y ≡ y₀ →
    y₀' ≡ +-match y' (n +ₗ zero)
  +-comm-helper₂-match n y nothing y₀ y₀' py py₀ ny =
    sym py₀ ∙ cong pred y₀≡n+0
\end{code}
\begin{code}[hide]
    where
    y₀≡n+0 = sym ny ∙ cong (n +ₗ_) (pred-inj py)
\end{code}
\begin{code}
  +-comm-helper₂-match n y (just y') y₀ nothing py py₀ ny
    = impossible
\end{code}
\begin{code}[hide]
    where
    impossible =
      exfalso
        (¬just≡nothing
          ( cong pred
              (sym (+ₗ-suc n y') ∙ cong (n +ₗ_) (pred-inj (sym py)) ∙ ny) ∙
            py₀))
\end{code}
\begin{code}
  +-comm-helper₂-match n y (just y') y₀ (just y₀') py py₀ ny
    = cong just (+-comm-helper₂ n y' y₀' n+y'≡y₀')
\end{code}
\begin{code}[hide]
    where
    n+y'≡y₀' =
      just-inj _ _
        ( cong pred
            (sym (+ₗ-suc n y') ∙ cong (n +ₗ_) (pred-inj (sym py)) ∙ ny) ∙
          py₀)
\end{code}
\end{AgdaSuppressSpace}

The commutativity of multiplication can be proved similarly by tracking the
steps taken using \AgdaFunction{\_+ₗ\_}.

Defining and proving things directly by guarded corecursion is difficult, since
we cannot reuse earlier definitions, such as addition in the case of the
definition of multiplication. Defining the corecursion and coinduction
principles and then using them can help with managing the proofs. Using the
inductively defined identity type instead of the cubical path type for equality
also helps, as we can pattern match on them.

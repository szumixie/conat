\begin{code}
{-# OPTIONS --safe --guardedness --cubical --no-postfix-projection #-}

module ConatLib2.Base where

open import ConatLib2.Type

open import Cubical.Data.Maybe
open import Cubical.Data.Sum
open import Cubical.Data.Sigma

coite : ∀{i}{B : Set i} → (B → Maybe B) → B → ℕ∞
pred (coite f b) with f b
... | nothing = nothing
... | just x  = just (coite f x)

suc : ℕ∞ → ℕ∞
pred (suc a) = just a

+-aux : ℕ∞ → ℕ∞ ⊎ ℕ∞ → Maybe (ℕ∞ ⊎ ℕ∞)
+-aux k (inl n) with pred n
+-aux k (inl n) | just n' = just (inl n')
+-aux k (inl n) | nothing with pred k
+-aux k (inl n) | nothing | just k' = just (inr k')
+-aux k (inl n) | nothing | nothing = nothing
+-aux _ (inr k) with pred k
+-aux _ (inr k) | just k' = just (inr k')
+-aux _ (inr k) | nothing = nothing

infixl 6 _+_
_+_ : ℕ∞ → ℕ∞ → ℕ∞
_+_ n k = coite (+-aux k) (inl n)

*-aux : ℕ∞ → ℕ∞ × ℕ∞ → Maybe (ℕ∞ × ℕ∞)
*-aux restore (e1 , e2) with pred e1
... | nothing = nothing
... | just e1' with pred e2
... | nothing = just (e1' , restore)
... | just e2' = just (e1 , e2')

infixl 7 _*_
_*_ : ℕ∞ → ℕ∞ → ℕ∞
n * k with pred n
... | nothing = unpred nothing
... | just n' with pred k
... | nothing = unpred nothing
... | just k' = coite (*-aux k') (n , k')
\end{code}

\begin{code}[hide]
{-# OPTIONS --safe --cubical --guardedness --no-import-sorts #-}
{-# OPTIONS -WnoUnsupportedIndexedMatch #-}

module ConatLib2.Bisimilarity.Base where

open import Agda.Primitive

open import Cubical.Data.Maybe
open import Cubical.Foundations.Prelude

open import Cubical.Data.Empty
  renaming (rec to exfalso)

open import ConatLib2.Type
open import ConatLib2.Inspect

private variable
  A : Type
  a a' : A
  n k : A
  x y z : A
\end{code}
To prove the commutativity of addition, we also define an embedding language
(based on Danielsson \cite{danielsson-beating} and Agda standard library's
\AgdaRecord{Stream} reasoning combinators~\cite{agda-stdlib}) to make the equational
reasoning possible by adding the symmetry and transitivity operations on
equations as constructors in this language, including the possibility to use
corecursive steps in a proof.

We define the language as an inductive relation indexed by conatural numbers.
This relation corresponds to the \AgdaDatatype{Expr} in
Section~\ref{sec:dsl-mul}.
\begin{code}[hide]
infix 4 _`≈_ _≈_
mutual
\end{code}
\begin{code}
  data _≈_ : ℕ∞ → ℕ∞ → Type where
    `eq     : x ≡ y → x ≈ y
    `step   : x `≈ y → x ≈ y
    `sym    : x ≈ y → y ≈ x
    `trans  : x ≈ y → y ≈ z → x ≈ z
\end{code}
\begin{AgdaMultiCode}%
We also mutually define a coinductive relation corresponding to
\AgdaRecord{NExpr} in Section~\ref{sec:dsl-mul}.
\begin{code}
  record _`≈_ (n k : ℕ∞) : Type where
    coinductive
\end{code}
\begin{code}[hide]
    constructor bisim
\end{code}
\begin{code}
    field
      pred : Maybe~ _≈_ (pred n) (pred k)
\end{code}
\end{AgdaMultiCode}
We use the pointwise relation on \AgdaDatatype{Maybe} values defined as follows:
\begin{code}
  data Maybe~ (A~ : A → A → Type) :
    Maybe A → Maybe A → Type
    where
      refl-nothing : Maybe~ A~ nothing nothing
      cong-just : A~ a a' → Maybe~ A~ (just a) (just a')
\end{code}
\begin{code}[hide]
open _`≈_ public
\end{code}

We define the predecessor over the new \AgdaDatatype{\_≈\_} relation and the
interpretation into equality analogously to the ones in
Section~\ref{sec:dsl-mul}.
\begin{code}
predᴱ : x ≈ y → Maybe~ _≈_ (pred x) (pred y)
interp : x ≈ y → x ≡ y
interp-match : Maybe~ _≈_ x y → x ≡ y
\end{code}
\begin{code}[hide]
predᴱ (`step x) = pred x
predᴱ {n} {k} (`eq x) with pred n | inspect pred n | pred k | inspect pred k
... | just n' | reveal eq1 | just k' | reveal eq2 = cong-just (`eq (just-inj _ _ (sym eq1 ∙ cong pred x ∙ eq2)))
... | just n' | reveal eq1 | nothing | reveal eq2 = exfalso (¬just≡nothing (sym eq1 ∙ cong pred x ∙ eq2))
... | nothing | reveal eq1 | just k' | reveal eq2 = exfalso (¬nothing≡just (sym eq1 ∙ cong pred x ∙ eq2))
... | nothing | reveal eq1 | nothing | reveal eq2 = refl-nothing
predᴱ {n} {k} (`sym e) with pred n | pred k | predᴱ {k} {n} e
... | nothing  | nothing  | _ = refl-nothing
... | .just n' | .just k' | cong-just x = cong-just (`sym x)
predᴱ (`trans {n} {m} {k} e1 e2) with pred n | pred m | pred k | predᴱ {n} {m} e1 | predᴱ {m} {k} e2
... | nothing  | nothing  | nothing  | _  | _  = refl-nothing
... | .just n' | .just m' | .just k' | cong-just t1 | cong-just t2 = cong-just (`trans t1 t2)
\end{code}
\begin{code}[hide]
pred (interp e i) = interp-match (predᴱ e) i
interp-match refl-nothing = refl
interp-match (cong-just e) = cong just (interp e)
\end{code}

Let us set up the notation for equational reasoning using Agda's standard
library's notation~\cite{agda-stdlib}.
\begin{AgdaSuppressSpace}
\begin{code}[hide]
infixr 2 _`↺⟨_⟩_ _`↺⟨_⟨_ _`≡⟨_⟩_ _`≡⟨_⟨_ _`≈⟨_⟩_ _`≈⟨_⟨_ _`∙_ _`≡⟨⟩_
infix  3 _`∎

pattern _`∙_  {a} ab bc = `trans {a} ab bc
\end{code}
\begin{code}
pattern _`↺⟨_⟩_ x xy yz = `trans {x} (`step xy) yz
\end{code}
\begin{code}[hide]
pattern _`↺⟨_⟨_ a ba bc = `trans {a} (`sym (`step ba)) bc
pattern _`≈⟨_⟩_ a ab bc = `trans {a} ab bc
pattern _`≈⟨_⟨_ a ba bc = `trans {a} (`sym ba) bc
\end{code}
\begin{code}
pattern _`≡⟨_⟩_ x xy yz = `trans {x} (`eq xy) yz
\end{code}
\begin{code}[hide]
pattern _`≡⟨_⟨_ a ba bc = `trans {a} (`sym (`eq ba)) bc
\end{code}
\end{AgdaSuppressSpace}
\begin{code}[hide]
_`≡⟨⟩_ : ∀ a {b} → a ≈ b → a ≈ b
a `≡⟨⟩ ab = `trans {a} (`eq refl) ab
{-# INLINE _`≡⟨⟩_ #-}
\end{code}
\begin{code}
_`∎ : ∀ a → a ≈ a
x `∎ = `eq {x} refl
\end{code}

\begin{code}[hide]
{-# OPTIONS --safe --guardedness --cubical --no-postfix-projections #-}

module ConatLib2.Properties where

open import ConatLib2.Type
open import ConatLib2.Base
open import ConatLib2.Inspect
open import ConatLib2.Bisimilarity.Base

open import Agda.Primitive

open import Cubical.Foundations.Prelude
open import Cubical.Data.Maybe
open import Cubical.Data.Sum
open import Cubical.Data.Sigma
open import Cubical.Data.Empty
  renaming (rec to exfalso)
open import Cubical.Data.Unit

unpred∘pred : ∀{n} → unpred (pred n) ≡ n
pred (unpred∘pred {n} i) = pred n

unpred∘just : ∀{n} → unpred (just n) ≡ suc n
pred (unpred∘just {n} i) = just n

∞+1≡∞ : suc ∞ ≡ ∞
pred (∞+1≡∞ i) = just ∞

+-aux-inr : ∀ w k → coite (+-aux w) (inr k) ≡ k
pred (+-aux-inr w k i) with pred k
... | nothing = nothing
... | just k' = just (+-aux-inr w k' i)

_+∞≡∞ : ∀ n → n + ∞ ≡ ∞
pred ((n +∞≡∞) i) with pred n
... | nothing = just (+-aux-inr ∞ ∞ i)
... | just x = just ((x +∞≡∞) i)

∞+_≡∞ : ∀ n → ∞ + n ≡ ∞
pred (∞+ n ≡∞ i) = just (∞+ n ≡∞ i)

idl+ : (n : ℕ∞) → unpred nothing + n ≡ n
pred (idl+ n i) with pred n
... | nothing = nothing
... | just n' = just (+-aux-inr n n' i)

idr+ : ∀ n → n + unpred nothing ≡ n
pred (idr+ n i) with pred n
... | nothing = nothing
... | just n' = just (idr+ n' i)

sucl+ : ∀ n k → suc n + k ≡ suc (n + k)
pred (sucl+ n k i) = just (n + k)

cong-+ᵣ : (a b c : ℕ∞) → a ≡ b → a + c ≡ b + c
pred (cong-+ᵣ a b c e i) with pred a | inspect pred a | pred b | inspect pred b
pred (cong-+ᵣ a b c e i) | nothing | eq1 | nothing | eq2 with pred c
pred (cong-+ᵣ a b c e i) | nothing | _   | nothing | _   | nothing = nothing
pred (cong-+ᵣ a b c e i) | nothing | eq1 | nothing | eq2 | just x = just (coite (+-aux c) (inr x))
pred (cong-+ᵣ a b c e i) | nothing | reveal eq1 | just b' | reveal eq2 with ¬nothing≡just (sym eq1 ∙ cong pred e ∙ eq2)
... | ()
pred (cong-+ᵣ a b c e i) | just a' | reveal eq1 | nothing | reveal eq2 with ¬nothing≡just (sym eq2 ∙ cong pred (sym e) ∙ eq1)
... | ()
pred (cong-+ᵣ a b c e i) | just a' | reveal eq1 | just b' | reveal eq2 = just (cong-+ᵣ a' b' c (just-inj _ _ (sym eq1 ∙ cong pred e ∙ eq2)) i)

cong-+ₗ : (a b c : ℕ∞) → a ≡ b → c + a ≡ c + b
pred (cong-+ₗ a b c e i) with pred c | inspect pred c
pred (cong-+ₗ a b c e i) | nothing | eq-c with pred a | inspect pred a | pred b | inspect pred b
pred (cong-+ₗ a b c e i) | nothing | _           | nothing | _           | nothing | _ = nothing
pred (cong-+ₗ a b c e i) | nothing | reveal eq-c | nothing | reveal eq-a | just b' | reveal eq-b with ¬nothing≡just (sym eq-a ∙ cong pred e ∙ eq-b)
... | ()
pred (cong-+ₗ a b c e i) | nothing | _ | just a' | reveal eq-a | nothing | reveal eq-b with ¬just≡nothing (sym eq-a ∙ cong pred e ∙ eq-b)
... | ()
pred (cong-+ₗ a b c e i) | nothing | _ | just a' | reveal eq-a | just b' | reveal eq-b with sym eq-a ∙ cong pred e ∙ eq-b
... | e' = just ((+-aux-inr a a' ∙ just-inj _ _ e' ∙ sym (+-aux-inr b b')) i)
pred (cong-+ₗ a b c e i) | just c' | _ = just (cong-+ₗ a b c' e i)

`sucr+ : ∀ n k → n + suc k `≋ suc (n + k)
`prove (`sucr+ n k) with pred n | inspect pred n
... | nothing | reveal eq1 = cong-just (`sym (`path (cong-+ᵣ _ _ k (runT (`sym (`path (unpred∘pred {n})) `◾ `path (cong unpred eq1)))) `◾ `path (idl+ k) `◾ `sym (`path (+-aux-inr _ _))))
... | just x  | reveal eq1 = cong-just (`step (`sucr+ x k) `◾ `sym (`path (sucl+ x k)) `◾ `path (cong-+ᵣ (suc x) n k (runT (`sym (`path unpred∘just) `◾ `path (cong unpred (sym eq1)) `◾ `path (unpred∘pred)))))

sucr+ : ∀ n k → n + suc k ≡ suc (n + k)
sucr+ n k = run (`sucr+ n k)

assoc+ : (a b c : ℕ∞) → (a + b) + c ≡ a + (b + c)
pred (assoc+ a b c i) with pred a
... | just x = just (assoc+ x b c i)
... | nothing with pred b
... | just x = just ((cong-+ᵣ (coite (+-aux b) (inr x)) x c (+-aux-inr b x) ∙ sym (+-aux-inr (b + c) (x + c))) i)
... | nothing with pred c
... | just x = just ((+-aux-inr c x ∙ sym (+-aux-inr (b + c) _ ∙ +-aux-inr c _)) i)
... | nothing = nothing

`comm+ : (a b : ℕ∞) → a + b `≋ b + a
`prove (`comm+ a b) with pred a | inspect pred a
`prove (`comm+ a b) | just a' | _ with pred b | inspect pred b
`prove (`comm+ a b) | just a' | reveal eq1 | just b' | reveal eq2 =
  let succ∞a' = runT (`sym (`path (unpred∘pred {a})) `◾ `path (cong unpred eq1) `◾ `path unpred∘just)
      succ∞b' = runT (`sym (`path (unpred∘pred {b})) `◾ `path (cong unpred eq2) `◾ `path unpred∘just)
  in cong-just (`path (cong-+ₗ _ _ a' succ∞b')
 `◾ `step (`comm+ a' (suc b'))
 `◾ `path (sucl+ b' a')
 `◾ `sym (`path (sucr+ b' a'))
 `◾ `sym (`path (cong-+ₗ _ _ b' succ∞a')))
`prove (`comm+ a b) | just a' | reveal eq1 | nothing | reveal eq2 with pred a
`prove (`comm+ a b) | just a' | reveal eq1 | nothing | reveal eq2 | nothing = exfalso (¬nothing≡just eq1)
`prove (`comm+ a b) | just a' | reveal eq1 | nothing | reveal eq2 | just x = cong-just
  (`path (cong-+ₗ b (unpred nothing) a' (runT (`sym (`path unpred∘pred) `◾ `path (cong unpred eq2))))
  `◾ `path (idr+ a')
  `◾ `sym (`path (+-aux-inr a x ∙ just-inj _ _ eq1)))
`prove (`comm+ a b) | nothing | _ with pred b
`prove (`comm+ a b) | nothing | reveal eq1 | just b' = cong-just (`path (+-aux-inr b b')
                                               `◾ `sym (`path (idr+ b'))
                                               `◾ `path (cong-+ₗ (unpred nothing) a b' (runT (`path (cong unpred (sym eq1)) `◾ `path unpred∘pred))))
`prove (`comm+ a b) | nothing | reveal eq1 | nothing with pred a
... | nothing = nothing-refl
... | just x = exfalso (¬nothing≡just (sym eq1))

comm+ : (a b : ℕ∞) → a + b ≡ b + a
comm+ a b = run (`comm+ a b)
\end{code}

\begin{code}[hide]
{-# OPTIONS --safe --guardedness --cubical --no-postfix-projections #-}

module ConatLib2.Properties where

open import ConatLib2.Type
open import ConatLib2.Base
open import ConatLib2.Inspect
open import ConatLib2.Bisimilarity.Base

open import Agda.Primitive

open import Cubical.Foundations.Prelude
open import Cubical.Data.Maybe
open import Cubical.Data.Sum
open import Cubical.Data.Sigma
open import Cubical.Data.Empty
  renaming (rec to exfalso)
open import Cubical.Data.Unit
\end{code}

In this section we show

\begin{code}[hide]
unpred∘pred : ∀{n} → unpred (pred n) ≡ n
pred (unpred∘pred {n} i) = pred n

unpred∘just : ∀{n} → unpred (just n) ≡ suc n
pred (unpred∘just {n} i) = just n

pred-inj : ∀{n k} → pred n ≡ pred k → n ≡ k
pred (pred-inj e i) = e i

idl+ : (n : ℕ∞) → zero + n ≡ n
pred (idl+ n i) = pred n

idr+ : ∀ n → n + zero ≡ n
pred (idr+ n i) with pred n
... | nothing = nothing
... | just n' = just (idr+ n' i)

sucl+ : ∀ n k → suc n + k ≡ suc (n + k)
pred (sucl+ n k i) = just (n + k)

`sucr+ : ∀ n k → n + suc k `≋ suc (n + k)
`prove (`sucr+ n k) with pred n | inspect pred n
... | nothing | reveal eq1 = cong-just (
  k
    `≡⟨ idl+ k ⟨
  zero + k
    `≡⟨ cong (_+ k) (pred-inj eq1) ⟨
  n + k `∎
  )
... | just x  | reveal eq1 = cong-just (
  x + suc k
    `↺⟨ `sucr+ x k ⟩
  suc (x + k)
    `≡⟨ sucl+ x k ⟨
  suc x + k
    `≡⟨ cong (_+ k) (sym (pred-inj eq1)) ⟩
  n + k `∎
  )
sucr+ : ∀ n k → n + suc k ≡ suc (n + k)
sucr+ n k = run (`sucr+ n k)

assoc+ : (a b c : ℕ∞) → (a + b) + c ≡ a + (b + c)
pred (assoc+ a b c i) with pred a
... | just x = just (assoc+ x b c i)
... | nothing = +-match (pred b) c

`comm+ : (a b : ℕ∞) → a + b `≋ b + a
`comm+-match : ∀ a a' b b' → a' ≡ pred a → b' ≡ pred b → Maybe~ _≋_ (+-match a' b) (+-match b' a)
`comm+-match a nothing b nothing eq1 eq2 = subst2 (Maybe~ _≋_) eq2 eq1 nothing-refl
`comm+-match a nothing b (just b') eq1 eq2 = subst (λ x → Maybe~ _≋_ x (just (b' + a))) eq2 (cong-just (`path (sym (idr+ b')) `∙ `path (cong (b' +_) (pred-inj eq1))))
`comm+-match a (just a') b nothing eq1 eq2 = subst (Maybe~ _≋_ (just (a' + b))) eq1 (cong-just (`path (cong (a' +_) (pred-inj (sym eq2))) `∙ `path (idr+ a')))
`comm+-match a (just a') b (just b') eq1 eq2 = cong-just (
  a' + b
    `≡⟨ cong (a' +_) (pred-inj (sym eq2)) ⟩
  a' + suc b'
    `↺⟨ `comm+ a' (suc b') ⟩
  suc b' + a'
    `≡⟨ sucl+ b' a' ⟩
  suc (b' + a')
    `≡⟨ sym (sucr+ b' a') ⟩
  b' + suc a'
    `≡⟨ cong (b' +_) (pred-inj eq1) ⟩
  b' + a `∎
  )
`prove (`comm+ a b) = `comm+-match a (pred a) b (pred b) refl refl
\end{code}

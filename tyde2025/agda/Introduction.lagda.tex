\begin{code}[hide]
{-# OPTIONS --cubical --guardedness --no-import-sorts --no-postfix-projections #-}

module Introduction where

open import Cubical.Foundations.Prelude
\end{code}
In dependent type theory, natural numbers are represented as an inductive type
with two constructors, one for zero and the other for the successor of a natural
number. In the Agda proof assistant, we write it as follows:
\begin{code}[hide]
module _ where private
\end{code}
\begin{code}
  data ℕ : Type where
    zero  :      ℕ
    suc   : ℕ →  ℕ
\end{code}
Categorically, natural numbers are the initial object in the category of
algebras over the $\mathbb{1} + {-}$ (or the ``Maybe'') endofunctor. So an
equivalent representation of natural numbers is an inductive type with a single
constructor containing a \AgdaDatatype{Maybe} of a natural number.
\begin{code}[hide]
module _ where private
\end{code}
\begin{code}
  data Maybe (A : Type) : Type where
    nothing  :      Maybe A
    just     : A →  Maybe A

  data ℕ : Type where
    con : Maybe ℕ → ℕ
\end{code}

We can dualise natural numbers to get \emph{conatural numbers}, which are the
terminal object in the category of coalgebras over the $\mathbb{1} + {-}$
endofunctor~\cite{hagino-categorical}. In Agda, it is a coinductive record type
with one destructor into \AgdaDatatype{Maybe} of conatural numbers.
\begin{code}[hide]
open import Cubical.Data.Maybe using (Maybe; nothing; just)
\end{code}
\begin{code}
record ℕ∞ : Type where
  coinductive
  field
    pred : Maybe ℕ∞
\end{code}
\begin{code}[hide]
open ℕ∞ public
\end{code}
This destructor is the predecessor function which either fails or returns
another conatural number.

We can define elements of \AgdaRecord{ℕ∞} by copattern
matching~\cite{abel-copatterns}, that is, we specify what the predecessor is for
a particular element. As examples, zero does not have a predecessor:
\begin{code}
zero : ℕ∞
pred zero = nothing
\end{code}
The predecessor of a successor of a number is just that number:
\begin{code}
suc : ℕ∞ → ℕ∞
pred (suc x) = just x
\end{code}
The predecessor of infinity is just infinity:
\begin{code}
∞ : ℕ∞
pred ∞ = just ∞
\end{code}
The above definition for \AgdaFunction{∞} is not structurally recursive, but it
is \emph{guarded}, that is, the recursive occurrence is after an instance of
copattern matching (\AgdaField{pred}), under only constructors
(\AgdaInductiveConstructor{just})~\cite{coquand-infinite}. Agda uses guardedness
to check whether a corecursive defintion is productive. A definition is
\emph{productive} when one can compute the application of any finite amount of
destructors on a corecursive value in a finite amount of steps. Guardedness is
sufficient to show productivity but it is not necessary, thus Agda is too
conservative about which corecursive definitions it allows.

The conatural numbers can represent all natural numbers and an extra element for
infinity, however, it is not constructively isomorphic to $\mathbb{N} +
\mathbb{1}$, because all functions out of the conatural numbers must be
continuous~\cite{escardo-infinite}. Computationally, finite amount of output can
only depend on finite amount of input. Topologically, \AgdaFunction{∞} is the
limit of $0, 1, 2, \dots$, which must be preserved. We can visualise the
topological space as follows:
\begin{center}
  \begin{tikzpicture}[
      scale=8,
      every node/.style={
        circle,
        fill,
        inner sep=0pt,
        minimum size=1mm,
        label position=south,
        label distance=1mm,
      }
    ]
    \path
    foreach \i in {0,...,7} {($({1-(2/3)^\i},0)$) node [label=\i] {}}
    ($({1-(2/3)^7/2},0)$) node [fill=none] {\dots}
    (1,0) node [label=∞] {};
  \end{tikzpicture}
\end{center}
As a result of this restriction, we cannot define a function that decides if an
element is equal to \AgdaFunction{∞}.

Our contribution in this paper is to prove that the conatural numbers form an
exponential commutative semiring by guarded corecursion, along the way showing
methods to keep the corecursion guarded. An \emph{exponential commutative
semiring} is a commutative semiring with a binary operation for exponentiation
satisfying the following equations:
\begin{align*}
  x^{yz} &= (x^y)^z & x^{y+z} &= x^y x^z & (xy)^z &= x^z y^z \\
  x^1 &= x & x^0 &= 1 & 1^x &= 1
\end{align*}

In Section~\ref{sec:problem}, we show how to define addition on conatural
numbers and prove that it is associative, at the same time, we show that if we
naïvely define multiplication and prove that addition is commutative, they get
rejected by Agda because they are not guarded. In the next sections, we show
three ways to avoid this issue, using multiplication and commutativity of
addition as running examples.

In section~\ref{sec:direct}, we directly use corecursion to define
multiplication and prove the commutativity of addition, avoiding reusing any
previous definitions to keep guardedness.

In section~\ref{sec:dsl}, we use Nils Anders Danielsson's
method~\cite{danielsson-beating} to use an embedded language to define
multiplication, and another language to the prove commutativity of addition.

In section~\ref{sec:quotiented}, we adapt the previous method to Cubical Agda,
making use of mixed higher-inductive/coinductive types and the univalence
principle to define and prove all the operations and equations of an exponential
commutative semiring at once.

\subsection{Formalisation}

In vanilla Agda, it is not possible to prove non-trivial equations about
conatural numbers using the Martin-Löf identity type~\cite{mcbride-unfold}, one
needs to either postulate the coinduction principle, or instead use a
coinductively defined equivalence relation, in which case one would need to
manually prove that operations preserve this relation. As such, in this paper we
use Cubical Agda, where the coinduction principle and other equations can be
directly proved thanks to the interaction between copattern matching and the
interval~\cite{vezzosi-cubical}. As an example, the proof below that the
predecessor function is injective cannot be done in vanilla Agda:
\begin{code}
pred-inj : ∀ {x y} → pred x ≡ pred y → x ≡ y
pred (pred-inj p i) = p i
\end{code}

With the method in Section~\ref{sec:direct}, we have formalised that the
conatural numbers form a commutative semiring, and defined the exponentiation
operation without equations. With the method in Section~\ref{sec:dsl} we have
proved the properties of addition. With the last method, in
Section~\ref{sec:quotiented}, we have a full formalisation that the conatural
numbers form an exponential commutative semiring.

Our formalisation is available at \url{https://github.com/szumixie/conat}.

\subsection{Related work}

There are some extensions to type theory that can make corecursive definitions
easier to define.

Using sized types, one can attach ordinals to constructors, which allows
corecursion to be done by well-founded recursion on the
sizes~\cite{hughes-sized, abel-delay}. This bypasses the guardedness
restriction, however, the current implementation of sized types in Agda is
inconsistent~\cite{abel-agda-sized}.

Guarded recursion adds a modality to type theory and a fixed point operation
which uses the modality to avoid allowing non-terminating
programs~\cite{nakano-modality,atkey-productive}.

There is an equivalent non-coinductive representation of conatural numbers,
which are monotonically decreasing (or increasing) Boolean
sequences~\cite{escardo-infinite}.
\[
  (f : \mathbb{N} \to \mathbb{2}) \times ((m\ n : \mathbb{N}) \to n \le m \to f(m) \le f(n))
\]
Naïm Camille Favier proved that conatural numbers form a semiring with meet
using this representation, avoiding corecursion~\cite{favier-conat}.
